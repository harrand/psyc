cgv_conv_undef ::= func(v : cgv, to : ty, prog : program mut&, loc : srcloc) -> cgv
{
	psyc_error_begin(loc);
	puts("conversion logic not setup properly for conversion from ");
	putchar('"');
	putty(v.type);
	putchar('"');
	puts(" to ");
	putchar('"');
	putty(to);
	putchar('"');
	puts(" - please report this as a compiler bug.");
	psyc_diag_end();
	psyc_exit_failure();
	return zero;
};

cgv_conv_cant ::= func(v : cgv, to : ty, prog : program mut&, loc : srcloc) -> cgv
{
	from ::= v.type;

	psyc_error_begin(loc);
	puts("no valid conversion from ");
	putchar('"');
	putty(from);
	putchar('"');
	puts(" to ");
	putchar('"');
	putty(to);
	putchar('"');
	psyc_diag_end();
	psyc_exit_failure();
	return zero;
};

cgv_conv_could_if_weak ::= func(v : cgv, to : ty, prog : program mut&, loc : srcloc) -> cgv
{
	from ::= v.type;

	psyc_error_begin(loc);
	puts("no valid conversion from ");
	putchar('"');
	putty(from);
	putchar('"');
	puts(" to ");
	putchar('"');
	putty(to);
	putchar('"');
	puts(" (but there could be if you casted it)");
	psyc_diag_end();
	psyc_exit_failure();
	return zero;
};

cgv_conv_i2i ::= func(v : cgv, to : ty, prog : program mut&, loc : srcloc) -> cgv
{
	ret : cgv mut := v;
	cgto ::= cg.type(to, prog);

	from ::= v.type;
	from_signed ::= ty_is_signed_integral(from);
	to_signed ::= ty_is_signed_integral(to);
	from_size ::= ty_integral_size(from);
	to_size ::= ty_integral_size(to);

	if(from_size < to_size)
	{
		// expanding to larger size
		if(from_signed)
		{
			(ret.ll) = LLVMBuildSExt(ir, v.ll, cgto, "");
		}
		if(!from_signed)
		{
			(ret.ll) = LLVMBuildZExt(ir, v.ll, cgto, "");
		}
	}
	if(from_size > to_size)
	{
		// truncating to smaller size
		(ret.ll) = LLVMBuildTrunc(ir, v.ll, cgto, "");
	}
	return ret;
};

cgv_conv_i2f ::= func(v : cgv, to : ty, prog : program mut&, loc : srcloc) -> cgv
{
	ret : cgv mut := v;
	cgto ::= cg.type(to, prog);

	from ::= v.type;

	from_signed ::= ty_is_signed_integral(from);
	if(from_signed)
	{
		(ret.ll) = LLVMBuildSIToFP(ir, v.ll, cgto, "");
	}
	if(!from_signed)
	{
		(ret.ll) = LLVMBuildUIToFP(ir, v.ll, cgto, "");
	}
	return ret;
};

cgv_conv_i2p ::= func(v : cgv, to : ty, prog : program mut&, loc : srcloc) -> cgv
{
	ret : cgv mut := v;
	(ret.ll) = LLVMBuildIntToPtr(ir, v.ll, cg.type(to, prog), "");
	return ret;
};

cgv_conv_i2e ::= func(v : cgv, to : ty, prog : program mut&, loc : srcloc) -> cgv
{
	// i.e is trivial
	transformed : cgv mut := cgv_conv_i2i(v, ty_getprim(primty.s64, tyqual.weak), prog, loc);
	(transformed.type) = to;
	return transformed;
};

cgv_conv_i2b ::= func(v : cgv, to : ty, prog : program mut&, loc : srcloc) -> cgv
{
	// always trunc
	ret : cgv mut := v;
	(ret.ll) = LLVMBuildTrunc(ir, v.ll, cg.type(to, prog), "");
	return ret;
};

cgv_conv_e2i ::= func(v : cgv, to : ty, prog : program mut&, loc : srcloc) -> cgv
{
	// pretend enumty is an s64
	transformed : cgv mut := v;
	tty ::= v.type;
	(transformed.type) = ty_getprim(primty.s64, tty.qual);
	return cgv_conv_i2i(transformed, to, prog, loc);
};

cgv_conv_p2i ::= func(v : cgv, to : ty, prog : program mut&, loc : srcloc) -> cgv
{
	ret : cgv mut := v;
	(ret.ll) = LLVMBuildPtrToInt(ir, v.ll, cg.type(to, prog), "");
	return ret;
};

cgv_conv_p2fn ::= func(v : cgv, to : ty, prog : program mut&, loc : srcloc) -> cgv
{
	ret : cgv mut := v;
	(ret.type) = to;
	return ret;
};

cgv_conv_p2p ::= func(v : cgv, to : ty, prog : program mut&, loc : srcloc) -> cgv
{
	ret : cgv mut := v;
	(ret.type) = to;
	return ret;
};

cgv_conv_fn2p ::= func(v : cgv, to : ty, prog : program mut&, loc : srcloc) -> cgv
{
	ret : cgv mut := v;
	(ret.type) = to;
	return ret;
};

cgv_conv_f2i ::= func(v : cgv, to : ty, prog : program mut&, loc : srcloc) -> cgv
{
	ret : cgv mut := v;
	cgto ::= cg.type(to, prog);

	to_signed ::= ty_is_signed_integral(to);
	if(to_signed)
	{
		(ret.ll) = LLVMBuildFPToSI(ir, v.ll, cgto, "");
	}
	if(!to_signed)
	{
		(ret.ll) = LLVMBuildFPToUI(ir, v.ll, cgto, "");
	}
	return ret;
};

cgv_conv_f2f ::= func(v : cgv, to : ty, prog : program mut&, loc : srcloc) -> cgv
{
	ret : cgv mut := v;
	cgto ::= cg.type(to, prog);

	from ::= v.type;
	from_size ::= ty_floating_point_size(from);
	to_size ::= ty_floating_point_size(to);
	if(from_size == to_size)
	{
		return ret;
	}
	if(from_size > to_size)
	{
		(ret.ll) = LLVMBuildFPTrunc(ir, v.ll, cgto, "");
	}
	if(from_size < to_size)
	{
		(ret.ll) = LLVMBuildFPExt(ir, v.ll, cgto, "");
	}

	return ret;
};

cgv_conv_b2i ::= func(v : cgv, to : ty, prog : program mut&, loc : srcloc) -> cgv
{
	// bool to another integral type
	// bool is guaranteed to be smaller, so its always zext
	ret : cgv mut := v;
	(ret.ll) = LLVMBuildZExt(ir, v.ll, cg.type(to, prog), "");
	return ret;
};

cgv_conv_same ::= func(v : cgv, to : ty, prog : program mut&, loc : srcloc) -> cgv
{
	return v;
};

== build ==
{
	add_source_file("codegen_value.psy");
}
