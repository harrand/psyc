mod : u64 mut;

ir : u64 mut;
db : u64 mut;
di_cu : u64 mut;

str2cstr ::= func(str : u8?, len : u64, a : arena mut? -> u8?)
{
	cstr : u8 mut? := arena_alloc(a, len + 1);
	memcopy(cstr, str, len);
	deref(cstr # len) = 0;
	return cstr;
};

cg_finish ::= func( -> v0)
{
	err : u8? mut;
	if(LLVMVerifyModule(mod, 2, ref err) != 0)
	{
		psyc_error_begin(zero);

		str ::= LLVMPrintModuleToString(mod);
		putzstr("Codegen: ");
		putchar(10);
		putzstr(str);
		putchar(10);
		putchar(10);

		putzstr("LLVM Internal Codegen Failed:");
		putchar(10);
		putzstr(err);
		putchar(10);

		psyc_diag_end();
		psyc_exit_failure();
	}
};

cg_primty ::= func(p : primty -> u64)
{
	arr : u64 mut[13];
	deref(arr # ((primty.s64)@s64)) = LLVMInt64Type();
	deref(arr # ((primty.s32)@s64)) = LLVMInt32Type();
	deref(arr # ((primty.s16)@s64)) = LLVMInt16Type();
	deref(arr # ((primty.s8)@s64)) = LLVMInt8Type();

	deref(arr # ((primty.u64)@s64)) = LLVMInt64Type();
	deref(arr # ((primty.u32)@s64)) = LLVMInt32Type();
	deref(arr # ((primty.u16)@s64)) = LLVMInt16Type();
	deref(arr # ((primty.u8)@s64)) = LLVMInt8Type();

	deref(arr # ((primty.bool)@s64)) = LLVMInt1Type();
	deref(arr # ((primty.f64)@s64)) = LLVMDoubleType();
	deref(arr # ((primty.f32)@s64)) = LLVMFloatType();

	deref(arr # ((primty.v0)@s64)) = LLVMVoidType();
	return deref(arr # (p@s64));
};

di_primty ::= func(p : primty -> u64)
{
	arr : u64 mut[13];
	deref(arr # ((primty.s64)@s64)) = LLVMDIBuilderCreateBasicType(db, "s64", 3, 64, DW_ATE_signed, zero);
	deref(arr # ((primty.s32)@s64)) = LLVMDIBuilderCreateBasicType(db, "s32", 3, 32, DW_ATE_signed, zero);
	deref(arr # ((primty.s16)@s64)) = LLVMDIBuilderCreateBasicType(db, "s16", 3, 16, DW_ATE_signed, zero);
	deref(arr # ((primty.s8)@s64)) = LLVMDIBuilderCreateBasicType(db, "s8", 3, 8, DW_ATE_signed_char, zero);

	deref(arr # ((primty.u64)@s64)) = LLVMDIBuilderCreateBasicType(db, "u64", 3, 64, DW_ATE_unsigned, zero);
	deref(arr # ((primty.u32)@s64)) = LLVMDIBuilderCreateBasicType(db, "u32", 3, 32, DW_ATE_unsigned, zero);
	deref(arr # ((primty.u16)@s64)) = LLVMDIBuilderCreateBasicType(db, "u16", 3, 16, DW_ATE_unsigned, zero);
	deref(arr # ((primty.u8)@s64)) = LLVMDIBuilderCreateBasicType(db, "u8", 3, 8, DW_ATE_unsigned_char, zero);

	deref(arr # ((primty.bool)@s64)) = LLVMDIBuilderCreateBasicType(db, "bool", 4, 1, DW_ATE_boolean, zero);
	deref(arr # ((primty.f64)@s64)) = LLVMDIBuilderCreateBasicType(db, "f64", 3, 64, DW_ATE_float, zero);
	deref(arr # ((primty.f32)@s64)) = LLVMDIBuilderCreateBasicType(db, "f32", 3, 32, DW_ATE_float, zero);

	deref(arr # ((primty.v0)@s64)) = LLVMDIBuilderCreateBasicType(db, "v0", 2, 0, DW_ATE_address, zero);
	return deref(arr # (p@s64));
};

cg_struct_ptr_t ::= struct
{
	fn : func(def : structdef mut?, prog : program mut? -> v0);
};
cg_struct_ptr : cg_struct_ptr_t mut;
// convert psyc 'ty' to LLVMTypeRef
cg_type ::= func(t : ty, prog : program mut? -> u64)
{
	if(ty_isbad(t))
	{
		psyc_panic(zero, "attempted to codegen cg_type(badtype). semal should've caught this. compiler bug.");
	}
	if((t.tag) == (tytag.prim))
	{
		return cg_primty(t.prim);
	}
	if((t.tag) == (tytag.enm))
	{
		return cg_primty(primty.s64);
	}
	if((t.tag) == (tytag.ptr))
	{
		ptr ::= t.ptr;
		u ::= deref((ptr.underlying)@ty?);
		cg_u ::= cg_type(u, prog);
		return LLVMPointerType(cg_u, 0);
	}
	if((t.tag) == (tytag.strct))
	{
		strct ::= t.strct;
		def ::= program_find_struct(prog, strct.name, strct.name_len);
		if((def->codegen) == 0)
		{
			// todo: need to cg_struct here.
			cg_struct_ptr.fn(def@_, prog);
		}
		return def->codegen;
	}
	if((t.tag) == (tytag.fn))
	{
		fn ::= t.fn;
		paramdata : u64 mut? := arena_alloc(global_arena, __sizeof(u64) * (fn.param_count));

		ret_cg ::= cg_type(deref((fn.return_type)@ty?), prog);
		param_tys ::= (fn.param_types)@ty?;

		i : u64 mut;
		for(i = 0, i < (fn.param_count), i = i + 1)
		{
			deref(paramdata # i) = cg_type(deref(param_tys # i), prog);
		}

		parcountu32 ::= (fn.param_count)@u32;
		return LLVMFunctionType(ret_cg, paramdata, parcountu32, false@s32);
	}
	if((t.tag) == (tytag.arr))
	{
		arr ::= t.arr;
		u_cg ::= cg_type(deref((arr.underlying)@ty?), prog);
		return LLVMArrayType2(u_cg, arr.len);
	}
	psyc_panic(zero, "control flow reached end of cg_type - compiler bug.");
	return 0;
};

di_type ::= func(t : ty, prog : program mut? -> u64)
{
	if(ty_isbad(t))
	{
		psyc_panic(zero, "attempted to codegen di_type(badtype). semal should've caught this. compiler bug.");
	}
	if((t.tag) == (tytag.prim))
	{
		return di_primty(t.prim);
	}
	if((t.tag) == (tytag.enm))
	{
		return di_primty(primty.s64);
	}
	if((t.tag) == (tytag.ptr))
	{
		ptr ::= t.ptr;
		u ::= deref((ptr.underlying)@ty?);
		di_u ::= di_type(u, prog);
		return LLVMDIBuilderCreatePointerType(db, di_u, 64, zero, zero, zero, zero);
	}
	if((t.tag) == (tytag.strct))
	{
		strct ::= t.strct;
		def ::= program_find_struct(prog, strct.name, strct.name_len);
		if((def->codegen) == 0)
		{
			// todo: need to cg_struct here.
			cg_struct_ptr.fn(def@_, prog);
		}
		return def->dbg;
	}
	if((t.tag) == (tytag.fn))
	{
		fn ::= t.fn;
		paramdata : u64 mut? := arena_alloc(global_arena, __sizeof(u64) * (fn.param_count + 1));

		ret_di ::= di_type(deref((fn.return_type)@ty?), prog);
		param_tys ::= (fn.param_types)@ty?;

		i : u64 mut;
		deref(paramdata # 0) = ret_di;
		for(i = 0, i < (fn.param_count), i = i + 1)
		{
			deref(paramdata # (i + 1)) = di_type(deref(param_tys # i), prog);
		}

		// todo: dont lie
		file_scope ::= prog->global;
		return LLVMDIBuilderCreateSubroutineType(db, file_scope.dbg, paramdata, fn.param_count + 1, zero);
	}
	if((t.tag) == (tytag.arr))
	{
		arr ::= t.arr;
		u_di ::= di_type(deref((arr.underlying)@ty?), prog);
		subrange ::= LLVMDIBuilderGetOrCreateSubrange(db, 0, arr.len);
		size ::= LLVMABISizeOfType(LLVMGetModuleDataLayout(mod), cg_type(t, prog));
		align ::= LLVMABIAlignmentOfType(LLVMGetModuleDataLayout(mod), cg_type(t, prog));
		return LLVMDIBuilderCreateArrayType(db, arr.len * size * 8, align * 8, u_di, ref subrange, 1);
	}
	psyc_panic(zero, "control flow reached end of di_type - compiler bug.");
	return 0;
};

cgu_is_defer ::= func(node : ast? -> bool)
{
	if(ast_is_expr(node, ast_expr_type.unop))
	{
		expr ::= node->expr;
		unop ::= expr.unop;
		if((unop.type) == (ast_unop_type.op_defer))
		{
			return true;
		}
	}
	return false;
};

cgu_is_ret ::= func(node : ast? -> bool)
{
	return ast_is_expr(node, ast_expr_type.ret);
};

cg_handle_defer ::= func(node : ast mut?, offset : u64, back_offset : u64 -> v0)
{
	childcount ::= node->childcount;
	pivot : u64 mut := childcount - back_offset - 1;
	// initially put pivot as the last node
	if((node->childcount) <= 1)
	{
		return;
	}

	while(cgu_is_ret(ast_get_child(node, pivot)))
	{
		if(pivot == 0)
		{
		}
		pivot = pivot - 1;
	}

	i : u64 mut;
	childptr ::= (node->children)@ast mut?;
	for(i = 0, i <= pivot, i = i)
	{
		child ::= ast_get_child(node, i);
		if(!cgu_is_defer(child))
		{
			i = i + 1;
		}
		if(cgu_is_defer(child))
		{
			// cache the defer'd thing and just remove the defer
			cache ::= deref ast_get_child(child@_, 0);
			// move range i+1-(pivot-1) left once, then copy cache just before pivot
			copy_len ::= pivot - i;
			memcopy(childptr # i, childptr # (i + 1), copy_len * __sizeof(ast));
			deref(childptr # pivot) = cache;
		}
	}
};

cg_children ::= func(ctx : context mut, prog : program mut?, offset : u64, back_offset : u64 -> v0)
{
	node ::= ctx.node;
	i : u64 mut;
	childcount ::= node->childcount;
	if(childcount == 0)
	{
		return;
	}
	cg_handle_defer(node@_, offset, back_offset);
	for(i = offset, i < (childcount - back_offset), i = i + 1)
	{
		cg_node(context
		{
			.node := ast_get_child(node@_, i);
			.local := ctx.local;
			.typecheck_only := ctx.typecheck_only;
		}, prog);
	}
};

cg_struct ::= func(def : structdef mut?, prog : program mut? -> v0)
{
	if((def->codegen) != 0)
	{
		return;
	}
	node ::= def->node;
	type ::= def->type;
	if(ty_isbad(type))
	{
		psyc_panic(node->loc, "cg_struct structdef's type was badtype. semal should've sorted this - compiler bug.");
	}
	strct ::= type.strct;
	memcount ::= strct.member_count;
	// make an array for all the members.
	memarr : u64 mut? := arena_alloc(global_arena, __sizeof(u64) * memcount);
	memtys ::= (strct.member_types)@ty?;
	i : u64 mut;
	for(i = 0, i < memcount, i = i + 1)
	{
		deref(memarr # i) = cg_type(deref(memtys # i), prog);
	}
	name_zstr : u8 mut? := arena_alloc(global_arena, 1 + (strct.name_len));
	memcopy(name_zstr, strct.name, strct.name_len);
	deref(name_zstr # (strct.name_len)) = 0;
	(def->codegen) = LLVMStructCreateNamed(LLVMGetGlobalContext(), name_zstr);
	LLVMStructSetBody(def->codegen, memarr, memcount@u32, false@s32);

	dbg_markup_struct(def, prog);
};

cg_declare_func ::= func(fn : function mut?, s : scope mut?, prog : program mut? -> v0)
{
	type ::= fn->type;
	fnty ::= type.fn;
	param_count ::= (fn->param_count);
	func_tycg ::= cg_type(type, prog);
	(fn->codegen) = LLVMAddFunction(mod, str2cstr(fn->name, fn->name_len, global_arena), func_tycg);

	param : u64 mut := LLVMGetFirstParam(fn->codegen);
	fnnode ::= fn->node;
	i : u64 mut;
	for(i = 0, i < param_count, i = i + 1)
	{
		param_node ::= ast_get_child(fnnode@_, i);
		param_decl ::= param_node->decl;
		param_zstr : u8 mut? := arena_alloc(global_arena, 1 + (param_decl.name_len));
		memcopy(param_zstr, param_decl.name, param_decl.name_len);
		deref(param_zstr # (param_decl.name_len)) = 0;
		LLVMSetValueName(param, param_zstr);
		param = LLVMGetNextParam(param);
	}
};

cg_asm ::= func(as : assembly mut?, s : scope mut?, prog : program mut? -> v0)
{
	type ::= as->type;
	fnty ::= type.fn;
	param_count ::= (as->param_count);
	func_tycg ::= cg_type(type, prog);
	(as->codegen) = LLVMGetInlineAsm(func_tycg, as->code, as->code_len, as->constraint, as->constraint_len, true@s32, false@s32, LLVMInlineAsmDialectATT, false@s32);
};

cg_define_func ::= func(fn : function mut?, s : scope mut?, prog : program mut? -> v0)
{
	if(fn->is_extern)
	{
		return;
	}

	// firstly, disable stack probes (NOCRT)
	gctx ::= LLVMGetGlobalContext();
	no_stack_probe_str ::= "no-stack-arg-probe";
	no_stack_probe ::= LLVMCreateStringAttribute(gctx, no_stack_probe_str, zstrlen(no_stack_probe_str)@u32, "", 0);
	LLVMAddAttributeAtIndex(fn->codegen, LLVMAttributeFunctionIndex, no_stack_probe);

	no_builtin_memset_str ::= "no-builtin-memset";
	no_builtin_memset ::= LLVMCreateStringAttribute(gctx, no_builtin_memset_str, zstrlen(no_builtin_memset_str)@u32, "", 0);
	LLVMAddAttributeAtIndex(fn->codegen, LLVMAttributeFunctionIndex, no_builtin_memset);
	no_builtin_memcpy_str ::= "no-builtin-memcpy";
	no_builtin_memcpy ::= LLVMCreateStringAttribute(gctx, no_builtin_memcpy_str, zstrlen(no_builtin_memcpy_str)@u32, "", 0);
	LLVMAddAttributeAtIndex(fn->codegen, LLVMAttributeFunctionIndex, no_builtin_memcpy);

	node ::= fn->node;
	funcscope ::= (fn->impl_scope)@scope mut?;
	(funcscope->codegen) = LLVMAppendBasicBlock(fn->codegen, "entry");
	LLVMPositionBuilderAtEnd(ir, funcscope->codegen);
	defer LLVMClearInsertionPosition(ir);

	dbg_markup_func(fn, prog);

	i : u64 mut;

	fntype ::= fn->type;
	fnpayload ::= fntype.fn;
	fnparamtys ::= (fnpayload.param_types)@ty?;

	// backing parameters is when you provide an extra alloca and store it.
	// if you dont do this then the parameter values are just ssa register values i.e immutable and un-referenceable (i.e ref param is wrong)
	// this kinda sucks, but backing things by default is a slight pessimisation
	// we absolutely have to back mutable parameters and arrays anyway, so im just gonna go ahead and back everything tbh.
	should_back : bool mut;
	for(i = 0, i < (fn->param_count), i = i + 1)
	{
		childnode ::= ast_get_child(node@_, i);
		childdecl ::= childnode->decl;
		childty ::= deref(fnparamtys # i);

		maybe_shadowing_var ::= scope_find_variable(funcscope, childdecl.name, childdecl.name_len);
		if(maybe_shadowing_var != zero)
		{
			varnode ::= maybe_shadowing_var->node;
			psyc_error_begin(varnode->loc);
			putzstr("local variable ");
			putchar('"');
			putbytes(childdecl.name, childdecl.name_len);
			putchar('"');
			putzstr(" would shadow enclosing function ");
			putchar('"');
			putbytes(fn->name, fn->name_len);
			putchar('"');
			putzstr("'s parameter ");
			putuint(i);
			putchar(' ');
			print_srcloc(childnode->loc);
			psyc_diag_end();
			psyc_exit_failure();
		}
		// yes, back this.
		should_back = false;
		if(ty_is(childty, tytag.arr))
		{
			should_back = true;
		}
		if(ty_hasqual(childty, tyqual.mut))
		{
			should_back = true;
		}
		if(should_back)
		{
			scope_declare_variable(funcscope, global_arena, variable
			{
				.node := childnode;
				.name := childdecl.name;
				.name_len := childdecl.name_len;
				.is_global := false;
				.type := childty;
				.internal := true;
				.initialiser := zero;
				.codegen := zero;
			});
			cg_decl(context
			{
				.node := childnode;
				.local := funcscope;
			}, prog);
			// now actually store into it.
			backed_array_var ::= scope_find_variable(funcscope, childdecl.name, childdecl.name_len);
			paramval ::= LLVMGetParam(fn->codegen, i@_);
			LLVMBuildStore(ir, paramval, backed_array_var->codegen);
		}
		if(ty_isbad(childty))
		{
			psyc_error_begin(childnode->loc);
			putzstr("parameter ");
			putchar('"');
			putbytes(childdecl.name, childdecl.name_len);
			putchar('"');
			putzstr(" yielded badtype");
			psyc_diag_end();
			psyc_exit_failure();
		}
	}

	// assume this contains all vars (recursively iterated) in all subscopes
	// a LLVM frontend should hoist (alloca all its local variables # function start), which is what we do here
	// instead of alloca'ing them as we see them naturally as decl stmts.
	vars ::= ref(funcscope->vars);
	for(i = 0, i < (vars->size), i = i + 1)
	{
		curvar ::= (vars->data) # i;
		if(!(curvar->internal))
		{
			cg_decl(context
			{
				.node := curvar->node;
				.local := funcscope;
				.typecheck_only := false;
			}, prog);
		}
	}

	child_iterate_offset ::= fn->param_count;
	cg_children(context
	{
		.node := node;
		.local := funcscope;
		.typecheck_only := false;
	}, prog, child_iterate_offset, 0);

	fn_retty ::= deref((fnpayload.return_type)@ty?);
	lastchildidx ::= (node->childcount) - 1;
	if((funcscope->terminator) != (ast_get_child(node@_, lastchildidx)))
	{
		// need a terminator
		if(ty_is_v0(fn_retty))
		{
			// add a retvoid here. (give it a vaguely valid debug location even though there is no corresponding line of code)
			dbg_markup_location(context
			{
				.node := node;
				.local := funcscope;
				.typecheck_only := false;
			}, prog);
			LLVMBuildRetVoid(ir);
		}
		if(!ty_is_v0(fn_retty))
		{
			psyc_error_begin(node->loc);
			putzstr("function ");
			putchar('"');
			putbytes(fn->name, fn->name_len);
			putchar('"');
			putzstr(" is missing a final return value");
			psyc_diag_end();
			psyc_exit_failure();
		}
	}
};

codegen ::= func(prog : program mut? -> v0)
{
	psyc_timed_begin(psyc_stage.codegen);
	defer psyc_timed_end();

	dbg_setup_builder(prog);
	args ::= ref(prog->compile_args);

	glob ::= ref(prog->global);
	structs ::= ref(glob->structs);
	funcs ::= ref(glob->funcs);
	asms ::= ref(glob->asms);
	gvars ::= ref(glob->vars);

	i : u64 mut;
	for(i = 0, i < (structs->size), i = i + 1)
	{
		cg_struct((structs->data) # i, prog);
	}

	for(i = 0, i < (funcs->size), i = i + 1)
	{
		cg_declare_func((funcs->data) # i, glob, prog);
	}

	for(i = 0, i < (asms->size), i = i + 1)
	{
		cg_asm((asms->data) # i, glob, prog);
	}

	for(i = 0, i < (gvars->size), i = i + 1)
	{
		gvar ::= (gvars->data) # i;
		(gvar->is_global) = true;
		cg_decl(context
		{
			.node := gvar->node;
			.local := glob;
			.typecheck_only := false;
		}, prog);
	}

	for(i = 0, i < (funcs->size), i = i + 1)
	{
		cg_define_func((funcs->data) # i, glob, prog);
	}

	fltused_cgty ::= cg_type(ty_getprim(primty.s64, zero), prog);
	fltused ::= LLVMAddGlobal(mod, fltused_cgty, "_fltused");
	LLVMSetLinkage(fltused, LLVMCommonLinkage);
	LLVMSetExternallyInitialized(fltused, 0);
	LLVMSetInitializer(fltused, LLVMConstNull(fltused_cgty));

	dbg_finalise(prog);
	cg_finish();
	(prog->codegen) = mod;
};

codegen_setup ::= func(-> v0)
{
	mod = LLVMModuleCreateWithName("Psy Program");
	ir = LLVMCreateBuilder();
	db = LLVMCreateDIBuilder(mod);
	codegen_node_setup();
	cg_struct_ptr = cg_struct_ptr_t{.fn := cg_struct;};

	i : u64 mut;
	// fill in cgv conv tables.
	cgv_conv_table_size ::= __sizeof(cgv_conv_table) / __sizeof(deref(cgv_conv_table # 0));
	for(i = 0, i < cgv_conv_table_size, i = i + 1)
	{
		deref(cgv_conv_table # i) = cgv_conv_fn{.fn := cgv_conv_undef;};
	}
	deref(cgv_conv_table # ((tyconv.cant)@s64)) = cgv_conv_fn{.fn := cgv_conv_cant;};
	deref(cgv_conv_table # ((tyconv.could_if_weak)@s64)) = cgv_conv_fn{.fn := cgv_conv_could_if_weak;};
	deref(cgv_conv_table # ((tyconv.i2i)@s64)) = cgv_conv_fn{.fn := cgv_conv_i2i;};
	deref(cgv_conv_table # ((tyconv.i2f)@s64)) = cgv_conv_fn{.fn := cgv_conv_i2f;};
	deref(cgv_conv_table # ((tyconv.i2p)@s64)) = cgv_conv_fn{.fn := cgv_conv_i2p;};
	deref(cgv_conv_table # ((tyconv.i2e)@s64)) = cgv_conv_fn{.fn := cgv_conv_i2e;};
	deref(cgv_conv_table # ((tyconv.i2b)@s64)) = cgv_conv_fn{.fn := cgv_conv_i2b;};
	deref(cgv_conv_table # ((tyconv.e2i)@s64)) = cgv_conv_fn{.fn := cgv_conv_e2i;};
	deref(cgv_conv_table # ((tyconv.p2i)@s64)) = cgv_conv_fn{.fn := cgv_conv_p2i;};
	deref(cgv_conv_table # ((tyconv.p2fn)@s64)) = cgv_conv_fn{.fn := cgv_conv_p2fn;};
	deref(cgv_conv_table # ((tyconv.p2p)@s64)) = cgv_conv_fn{.fn := cgv_conv_p2p;};
	deref(cgv_conv_table # ((tyconv.fn2p)@s64)) = cgv_conv_fn{.fn := cgv_conv_fn2p;};
	deref(cgv_conv_table # ((tyconv.f2i)@s64)) = cgv_conv_fn{.fn := cgv_conv_f2i;};
	deref(cgv_conv_table # ((tyconv.f2f)@s64)) = cgv_conv_fn{.fn := cgv_conv_f2f;};
	deref(cgv_conv_table # ((tyconv.b2i)@s64)) = cgv_conv_fn{.fn := cgv_conv_b2i;};
	deref(cgv_conv_table # ((tyconv.same)@s64)) = cgv_conv_fn{.fn := cgv_conv_same;};

	cg_builtin_table_size ::= __sizeof(cg_builtin_table) / __sizeof(deref(cg_builtin_table # 0));
	for(i = 0, i < cg_builtin_table_size, i = i + 1)
	{
		deref(cg_builtin_table # i) = cg_builtin_fn{.fn := cg_builtinfn_error;};
	}
	deref(cg_builtin_table # ((builtin.debugbreak)@s64)) = cg_builtin_fn{.fn := cg_builtin_debugbreak;};
	deref(cg_builtin_table # ((builtin.sizeof)@s64)) = cg_builtin_fn{.fn := cg_builtin_sizeof;};
	deref(cg_builtin_table # ((builtin.alignof)@s64)) = cg_builtin_fn{.fn := cg_builtin_alignof;};
	deref(cg_builtin_table # ((builtin.arrcpy)@s64)) = cg_builtin_fn{.fn := cg_builtin_arrcpy;};
	deref(cg_builtin_table # ((builtin.enumname)@s64)) = cg_builtin_fn{.fn := cg_builtin_enumname;};
	deref(cg_builtin_table # ((builtin.embed)@s64)) = cg_builtin_fn{.fn := cg_builtin_embed;};
	deref(cg_builtin_table # ((builtin.curloc)@s64)) = cg_builtin_fn{.fn := cg_builtin_curloc;};
	deref(cg_builtin_table # ((builtin.error)@s64)) = cg_builtin_fn{.fn := cg_builtin_error;};
};

== build ==
{
	add_source_file("llvm.psy");
	add_source_file("program.psy");

	add_source_file("codegen_node.psy");
	add_source_file("codegen_conv.psy");
	add_source_file("codegen_builtin.psy");
}
