cgv_conv_undef ::= func(v : cgv, to : ty, loc : srcloc -> cgv)
{
	psyc_error_begin(loc);
	putzstr("conversion logic not setup properly for conversion from ");
	putchar('"');
	putty(v.type);
	putchar('"');
	putzstr(" to ");
	putchar('"');
	putty(to);
	putchar('"');
	putzstr(" - please report this as a compiler bug.");
	psyc_diag_end();
	psyc_exit_failure();
	return zero;
};

cgv_conv_cant ::= func(v : cgv, to : ty, loc : srcloc -> cgv)
{
	from ::= v.type;

	psyc_error_begin(loc);
	putzstr("no valid conversion from ");
	putchar('"');
	putty(from);
	putchar('"');
	putzstr(" to ");
	putchar('"');
	putty(to);
	putchar('"');
	psyc_diag_end();
	psyc_exit_failure();
	return zero;
};

cgv_conv_could_if_weak ::= func(v : cgv, to : ty, loc : srcloc -> cgv)
{
	from ::= v.type;

	psyc_error_begin(loc);
	putzstr("no valid conversion from ");
	putchar('"');
	putty(from);
	putchar('"');
	putzstr(" to ");
	putchar('"');
	putty(to);
	putchar('"');
	putzstr(" (but there could be if you casted it)");
	psyc_diag_end();
	psyc_exit_failure();
	return zero;
};

cgv_conv_i2i ::= func(v : cgv, to : ty, loc : srcloc -> cgv)
{
	constant ::= LLVMIsConstant(v.ll)@bool;
	ret : cgv mut := v;
	cgto ::= cg_type(to);

	from ::= v.type;
	from_signed ::= ty_is_signed_integral(from);
	to_signed ::= ty_is_signed_integral(to);
	from_size ::= ty_integral_size(from);
	to_size ::= ty_integral_size(to);

	llv ::= LLVMConstIntGetSExtValue(v.ll);
	if(from_size < to_size)
	{
		// expanding to larger size
		if(from_signed)
		{
			if(constant)
			{
				(ret.ll) = LLVMConstInt(cgto, llv, true@s32);
			}
			if(!constant)
			{
				(ret.ll) = LLVMBuildSExt(ir, v.ll, cgto, "");
			}
		}
		if(!from_signed)
		{
			if(constant)
			{
				(ret.ll) = LLVMConstInt(cgto, llv, false@s32);
			}
			if(!constant)
			{
				(ret.ll) = LLVMBuildZExt(ir, v.ll, cgto, "");
			}
		}
	}
	if(from_size > to_size)
	{
		// truncating to smaller size
		if(constant)
		{
			(ret.ll) = LLVMConstInt(cgto, llv, false@s32);
		}
		if(!constant)
		{
			(ret.ll) = LLVMBuildTrunc(ir, v.ll, cgto, "");
		}
	}
	return ret;
};

cgv_conv_i2f ::= func(v : cgv, to : ty, loc : srcloc -> cgv)
{
	constant ::= LLVMIsConstant(v.ll)@bool;
	ret : cgv mut := v;
	cgto ::= cg_type(to);

	from ::= v.type;

	if(constant)
	{
		(ret.ll) = LLVMConstReal(cgto, LLVMConstIntGetSExtValue(v.ll));
		return ret;
	}
	from_signed ::= ty_is_signed_integral(from);
	if(from_signed)
	{
		(ret.ll) = LLVMBuildSIToFP(ir, v.ll, cgto, "");
	}
	if(!from_signed)
	{
		(ret.ll) = LLVMBuildUIToFP(ir, v.ll, cgto, "");
	}
	return ret;
};

cgv_conv_i2p ::= func(v : cgv, to : ty, loc : srcloc -> cgv)
{
	constant ::= LLVMIsConstant(v.ll)@bool;
	ret : cgv mut := v;
	if(constant)
	{
		(ret.ll) = LLVMConstIntToPtr(v.ll, cg_type(to));
		return ret;
	}
	(ret.ll) = LLVMBuildIntToPtr(ir, v.ll, cg_type(to), "");
	return ret;
};

cgv_conv_i2e ::= func(v : cgv, to : ty, loc : srcloc -> cgv)
{
	// i.e is trivial
	transformed : cgv mut := cgv_conv_i2i(v, ty_getprim(primty.s64, tyqual.weak), loc);
	(transformed.type) = to;
	return transformed;
};

cgv_conv_i2b ::= func(v : cgv, to : ty, loc : srcloc -> cgv)
{
	constant ::= LLVMIsConstant(v.ll)@bool;
	// always trunc
	ret : cgv mut := v;
	if(constant)
	{
		(ret.ll) = LLVMConstInt(cg_type(to), LLVMConstIntGetSExtValue(v.ll), false@s32);
		return ret;
	}
	(ret.ll) = LLVMBuildTrunc(ir, v.ll, cg_type(to), "");
	return ret;
};

cgv_conv_e2i ::= func(v : cgv, to : ty, loc : srcloc -> cgv)
{
	// pretend enumty is an s64
	transformed : cgv mut := v;
	tty ::= v.type;
	(transformed.type) = ty_getprim(primty.s64, tty.qual);
	return cgv_conv_i2i(transformed, to, loc);
};

cgv_conv_p2i ::= func(v : cgv, to : ty, loc : srcloc -> cgv)
{
	constant ::= LLVMIsConstant(v.ll)@bool;
	ret : cgv mut := v;
	if(constant)
	{
		(ret.ll) = LLVMConstPtrToInt(v.ll, cg_type(to));
		return ret;
	}
	(ret.ll) = LLVMBuildPtrToInt(ir, v.ll, cg_type(to), "");
	return ret;
};

cgv_conv_p2fn ::= func(v : cgv, to : ty, loc : srcloc -> cgv)
{
	ret : cgv mut := v;
	(ret.type) = to;
	return ret;
};

cgv_conv_p2p ::= func(v : cgv, to : ty, loc : srcloc -> cgv)
{
	ret : cgv mut := v;
	(ret.type) = to;
	return ret;
};

cgv_conv_fn2p ::= func(v : cgv, to : ty, loc : srcloc -> cgv)
{
	ret : cgv mut := v;
	(ret.type) = to;
	return ret;
};

cgv_conv_f2i ::= func(v : cgv, to : ty, loc : srcloc -> cgv)
{
	constant ::= LLVMIsConstant(v.ll)@bool;
	ret : cgv mut := v;
	cgto ::= cg_type(to);
	loses_info : s32 mut;

	if(constant)
	{
		(ret.ll) = LLVMConstInt(cgto, LLVMConstRealGetDouble(v.ll, ref loses_info)@_, true@s32);
		if(loses_info != 0)
		{
			psyc_panic(loc, "loss-of-double precision");
		}
		return ret;
	}
	to_signed ::= ty_is_signed_integral(to);
	if(to_signed)
	{
		(ret.ll) = LLVMBuildFPToSI(ir, v.ll, cgto, "");
	}
	if(!to_signed)
	{
		(ret.ll) = LLVMBuildFPToUI(ir, v.ll, cgto, "");
	}
	return ret;
};

cgv_conv_f2f ::= func(v : cgv, to : ty, loc : srcloc -> cgv)
{
	constant ::= LLVMIsConstant(v.ll)@bool;
	ret : cgv mut := v;
	cgto ::= cg_type(to);
	loses_info : s32 mut;

	if(constant)
	{
		(ret.ll) = LLVMConstReal(cgto, LLVMConstRealGetDouble(v.ll, ref loses_info)@_);
		if(loses_info != 0)
		{
			psyc_panic(loc, "loss-of-double precision");
		}
		return ret;
	}
	from ::= v.type;
	from_size ::= ty_floating_point_size(from);
	to_size ::= ty_floating_point_size(to);
	if(from_size == to_size)
	{
		return ret;
	}
	if(from_size > to_size)
	{
		(ret.ll) = LLVMBuildFPTrunc(ir, v.ll, cgto, "");
	}
	if(from_size < to_size)
	{
		(ret.ll) = LLVMBuildFPExt(ir, v.ll, cgto, "");
	}

	return ret;
};

cgv_conv_b2i ::= func(v : cgv, to : ty, loc : srcloc -> cgv)
{
	constant ::= LLVMIsConstant(v.ll)@bool;
	// bool to another integral type
	// bool is guaranteed to be smaller, so its always zext
	ret : cgv mut := v;
	if(constant)
	{
		(ret.ll) = LLVMConstInt(cg_type(to), LLVMConstIntGetSExtValue(v.ll), false@s32);
		return ret;
	}
	(ret.ll) = LLVMBuildZExt(ir, v.ll, cg_type(to), "");
	return ret;
};

cgv_conv_same ::= func(v : cgv, to : ty, loc : srcloc -> cgv)
{
	return v;
};

codegen_conv_setup ::= func(-> v0)
{
	i : u64 mut;
	// fill in cgv conv tables.
	cgv_conv_table_size ::= __sizeof(cgv_conv_table) / __sizeof(deref(cgv_conv_table # 0));
	for(i = 0, i < cgv_conv_table_size, i = i + 1)
	{
		deref(cgv_conv_table # i) = cgv_conv_fn{.fn := cgv_conv_undef;};
	}
	deref(cgv_conv_table # ((tyconv.cant)@s64)) = cgv_conv_fn{.fn := cgv_conv_cant;};
	deref(cgv_conv_table # ((tyconv.could_if_weak)@s64)) = cgv_conv_fn{.fn := cgv_conv_could_if_weak;};
	deref(cgv_conv_table # ((tyconv.i2i)@s64)) = cgv_conv_fn{.fn := cgv_conv_i2i;};
	deref(cgv_conv_table # ((tyconv.i2f)@s64)) = cgv_conv_fn{.fn := cgv_conv_i2f;};
	deref(cgv_conv_table # ((tyconv.i2p)@s64)) = cgv_conv_fn{.fn := cgv_conv_i2p;};
	deref(cgv_conv_table # ((tyconv.i2e)@s64)) = cgv_conv_fn{.fn := cgv_conv_i2e;};
	deref(cgv_conv_table # ((tyconv.i2b)@s64)) = cgv_conv_fn{.fn := cgv_conv_i2b;};
	deref(cgv_conv_table # ((tyconv.e2i)@s64)) = cgv_conv_fn{.fn := cgv_conv_e2i;};
	deref(cgv_conv_table # ((tyconv.p2i)@s64)) = cgv_conv_fn{.fn := cgv_conv_p2i;};
	deref(cgv_conv_table # ((tyconv.p2fn)@s64)) = cgv_conv_fn{.fn := cgv_conv_p2fn;};
	deref(cgv_conv_table # ((tyconv.p2p)@s64)) = cgv_conv_fn{.fn := cgv_conv_p2p;};
	deref(cgv_conv_table # ((tyconv.fn2p)@s64)) = cgv_conv_fn{.fn := cgv_conv_fn2p;};
	deref(cgv_conv_table # ((tyconv.f2i)@s64)) = cgv_conv_fn{.fn := cgv_conv_f2i;};
	deref(cgv_conv_table # ((tyconv.f2f)@s64)) = cgv_conv_fn{.fn := cgv_conv_f2f;};
	deref(cgv_conv_table # ((tyconv.b2i)@s64)) = cgv_conv_fn{.fn := cgv_conv_b2i;};
	deref(cgv_conv_table # ((tyconv.same)@s64)) = cgv_conv_fn{.fn := cgv_conv_same;};
	
};
