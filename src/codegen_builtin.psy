debugtrap_fn : u64 mut;

cg_builtin_debugbreak ::= func(ctx : context mut -> cgv)
{
	node ::= ctx.node;
	
	fn_type ::= LLVMFunctionType(LLVMVoidType(), zero, 0, false@s32);
	if(debugtrap_fn == 0)
	{
		debugtrap_fn = LLVMAddFunction(mod, "llvm.debugtrap", fn_type);
	}
	LLVMBuildCall2(ir, fn_type, debugtrap_fn, zero, 0, "");
	return zero;
};

cg_builtin_sizeof ::= func(ctx : context mut -> cgv)
{
	type : ty mut := zero;

	dstnode ::= ast_get_child((ctx.node)@_, 0);
	if(ast_is_expr(dstnode, ast_expr_type.symbol))
	{
		expr ::= dstnode->expr;
		sym ::= expr.symbol;
		type = scope_parse_typename(ctx.local, sym.symbol, sym.len);
	}
	if(ty_isbad(type))
	{
		val : cgv mut := cg_expr(context
		{
			.node := dstnode;
			.local := ctx.local;
			.typecheck_only := true;
		});
		type = (val.type);
	}

	dl ::= LLVMGetModuleDataLayout(mod);
	return cg_literal(make_literal_expr(ast_literal_expr
	{
		.type := ast_literal_type.integral;
		.integral := LLVMABISizeOfType(dl, cg_type(type))@_;
	}), ctx);
};

cg_builtin_alignof ::= func(ctx : context mut -> cgv)
{
	type : ty mut := zero;

	dstnode ::= ast_get_child((ctx.node)@_, 0);
	if(ast_is_expr(dstnode, ast_expr_type.symbol))
	{
		expr ::= dstnode->expr;
		sym ::= expr.symbol;
		type = scope_parse_typename(ctx.local, sym.symbol, sym.len);
	}
	if(ty_isbad(type))
	{
		val : cgv mut := cg_expr(context
		{
			.node := dstnode;
			.local := ctx.local;
			.typecheck_only := true;
		});
		type = (val.type);
	}

	dl ::= LLVMGetModuleDataLayout(mod);
	return cg_literal(make_literal_expr(ast_literal_expr
	{
		.type := ast_literal_type.integral;
		.integral := LLVMABIAlignmentOfType(dl, cg_type(type))@_;
	}), ctx);
};

cg_builtin_stack_push ::= func(ctx : context mut -> cgv)
{
	countnode ::= ast_get_child((ctx.node)@_, 0);
	countval : cgv mut := cg_expr(context
	{
		.node := countnode;
		.local := ctx.local;
		.typecheck_only := ctx.typecheck_only;
	});
	cgv_load_if_variable(ref countval);
	if(!ty_is_integral(countval.type))
	{
		psyc_error_begin(countnode->loc);
		putzstr("parameter to __stack_push must be an integral type, you have provided a ");
		putchar('"');
		putty(countval.type);
		putchar('"');
		psyc_diag_end();
		psyc_exit_failure();
	}

	retty ::= ty_ref(ty_getprim(primty.u8, tyqual.mut), tyqual.weak);
	ll : u64 mut := zero;
	if(!(ctx.typecheck_only))
	{
		ll = LLVMBuildArrayAlloca(ir, cg_type(retty), countval.ll, "__alloca");
	}
	return cgv
	{
		.tag := cgvtag.misc;
		.ll := ll;
		.type := retty;
	};
};

cg_builtin_arrcpy ::= func(ctx : context mut -> cgv)
{
	if(ctx.typecheck_only)
	{
		return zero;
	}
	node ::= ctx.node;
	if((node->childcount) < 2)
	{
		psyc_error_begin(node->loc);
		putzstr("__arrcpy requires # least 2 parameters");
		psyc_diag_end();
		psyc_exit_failure();
	}
	arrlen ::= (node->childcount) - 2;
	// first param is a pointer to something mutable
	// second param is a typename representing a type of the array
	// rest are the array values to copy...
	firstnode ::= ast_get_child(node@_, 0);
	ptr : cgv mut := cg_expr(context
	{
		.node := firstnode;
		.local := ctx.local;
	});
	cgv_load_if_variable(ref ptr);
	if(!ty_is(ptr.type, tytag.ptr))
	{
		psyc_error_begin(firstnode->loc);
		putzstr("parameter 0 to __arrcpy must be a pointer type, you have passed a ");
		putchar('"');
		putty(ptr.type);
		putchar('"');
		psyc_diag_end();
		psyc_exit_failure();
	}

	secondnode ::= ast_get_child(node@_, 1);
	if(!ast_is_expr(secondnode, ast_expr_type.symbol))
	{
		psyc_error_begin(firstnode->loc);
		putzstr("parameter 1 to __arrcpy must be a valid typename represented as a symbol expression");
		psyc_diag_end();
		psyc_exit_failure();
	}
	expr ::= secondnode->expr;
	sym ::= expr.symbol;
	type : ty mut := scope_parse_typename(ctx.local, sym.symbol, sym.len);
	if(ty_isbad(type))
	{
		psyc_error_begin(firstnode->loc);
		putzstr("parameter 1 to __arrcpy must be a valid typename - ");
		putchar('"');
		putbytes(sym.symbol, sym.len);
		putchar('"');
		putzstr(" yielded badtype");
		psyc_diag_end();
		psyc_exit_failure();
	}
	(type.qual) = (type.qual) | (tyqual.mut);

	index_list : u64 mut[2];
	deref(index_list # 0) = LLVMConstInt(LLVMInt64Type(), 0, false@s32);
	i : u64 mut;
	offset_ll : u64 mut;
	curnode : ast mut? mut;
	curval : cgv mut;
	for(i = 0, i < arrlen, i = i + 1)
	{
		deref(index_list # 1) = LLVMConstInt(cg_type(ty_getprim(primty.s64, zero)), i, false@s32);
		offset_ll = LLVMBuildGEP2(ir, cg_type(type), ptr.ll, index_list # 1, 1, "");

		curnode = ast_get_child(node@_, 2 + i);
		
		curval = cg_expr(context
		{
			.node := curnode;
			.local := ctx.local;
			.typecheck_only := false;
		});
		
		curval = cgv_convert(curval, type, curnode->loc, false);
		LLVMBuildStore(ir, curval.ll, offset_ll);
	}
	return cg_literal(make_literal_expr(ast_literal_expr
	{
		.type := ast_literal_type.integral;
		.integral := arrlen@_;
	}), ctx);
};

cg_builtin_enumname ::= func(ctx : context mut -> cgv)
{
	retty ::= ty_ref(ty_getprim(primty.u8, zero), zero);
	if(ctx.typecheck_only)
	{
		return cgv
		{
			.tag := zero;
			.ll := zero;
			.type := retty;
		};
	}
	node ::= ctx.node;
	enumnode ::= ast_get_child(node@_, 0);
	enumval : cgv mut := cg_expr(context
	{
		.node := enumnode;
		.local := ctx.local;
		.typecheck_only := false;
	});
	cgv_load_if_variable(ref enumval);
	if(!ty_is(enumval.type, tytag.enm))
	{
		psyc_error_begin(enumnode->loc);
		putzstr("parameter to __enumname must be an enum type, you have provided a ");
		putchar('"');
		putty(enumval.type);
		putchar('"');
		psyc_diag_end();
		psyc_exit_failure();
	}
	fn ::= scope_get_parent_function(ctx.local);

	type ::= enumval.type;
	enm ::= type.enm;
	llty ::= cg_type(retty);
	ll ::= LLVMBuildAlloca(ir, llty, "__enumname_dynamic");
	badenumval ::= LLVMBuildGlobalString(ir, "badenum", "badenum");
	LLVMBuildStore(ir, badenumval, ll);
	i : u64 mut;
	for(i = 0, i < (enm.entry_count), i = i + 1)
	{
		val ::= deref((enm.entry_values) # i);
		name ::= deref((enm.entry_names) # i);
		name_len ::= deref((enm.entry_name_lens) # i);
		val_ll ::= LLVMConstInt(cg_type(ty_getprim(primty.s64, zero)), val, false@s32);
		cond ::= LLVMBuildICmp(ir, 32, val_ll, enumval.ll, "");

		cond_blk ::= LLVMAppendBasicBlock(fn->codegen, "enumname_if");
		cont_blk ::= LLVMAppendBasicBlock(fn->codegen, "__enumname cont");

		LLVMBuildCondBr(ir, cond, cond_blk, cont_blk);
		LLVMPositionBuilderAtEnd(ir, cond_blk);
		// store this value into the alloca
		zstr : u8 mut? mut := arena_alloc(global_arena, name_len + 1);
		memcopy(zstr, name, name_len);
		deref(zstr # name_len) = 0;
		strval ::= LLVMBuildGlobalString(ir, zstr, "");
		LLVMBuildStore(ir, strval, ll);
		LLVMBuildBr(ir, cont_blk);
		LLVMPositionBuilderAtEnd(ir, cont_blk);
	}
	return cgv
	{
		.tag := cgvtag.misc;
		.ll := LLVMBuildLoad2(ir, llty, ll, "");
		.type := retty;
	};
};

cg_builtin_embed ::= func(ctx : context mut -> cgv)
{
	node ::= ctx.node;
	pathnode ::= ast_get_child(node@_, 0);
	path ::= sval_node(context
	{
		.node := pathnode;
		.local := ctx.local;
	});
	if((path.tag) != (sval_tag.literal))
	{
		psyc_error_begin(node->loc);
		putzstr("Invalid call to builtin - param 0 must be a literal");
		psyc_diag_end();
		psyc_exit_failure();
	}
	lit ::= path.literal;
	if((lit.type) != (ast_literal_type.string_literal))
	{
		psyc_error_begin(node->loc);
		putzstr("Invalid call to builtin - param 0 must be a string literal");
		psyc_diag_end();
		psyc_exit_failure();
	}

	zstr ::= sstr2zstr(lit.chars, lit.chars_len, global_arena);
	if(!file_exists(zstr))
	{
		psyc_error_begin(pathnode->loc);
		putzstr("Cannot embed file ");
		putchar('"');
		putzstr(zstr);
		putchar('"');
		putzstr(" as the file cannot be found");
		psyc_diag_end();
		psyc_exit_failure();
	}
	buflen ::= file_size_bytes(zstr);
	buf : u8 mut? := arena_alloc(global_arena, buflen);
	file_read(zstr, buf, buflen);

	result_node : ast mut := deref node;
	result_node.children = zero;
	result_node.childcount = 0;
	result_node.type = ast_type.init;
	embed_data_tyname ::= "embed_data";
	result_node.init = ast_init
	{
		.name := embed_data_tyname;
		.name_len := zstrlen(embed_data_tyname);
	};
	
	data_node : ast mut := deref node;
	data_node.children = zero;
	data_node.childcount = 0;
	data_node.type = ast_type.init;
	data_node.init = ast_init
	{
		.name := "data";
		.name_len := zstrlen("data");
	};
	data_value ::= ast
	{
		.type := ast_type.expr;
		.expr := make_literal_expr(ast_literal_expr
		{
.type := ast_literal_type.string_literal;
			.chars := buf;
			.chars_len := buflen;
		});
	};
	ast_add_child(ref data_node, data_value, global_arena);

	size_node : ast mut := deref node;
	size_node.children = zero;
	size_node.childcount = 0;
	size_node.type = ast_type.init;
	size_node.init = ast_init
	{
		.name := "size";
		.name_len := zstrlen("size");
	};
	size_value ::= ast
	{
		.type := ast_type.expr;
		.expr := make_literal_expr(ast_literal_expr
		{
			.type := ast_literal_type.integral;
			.integral := buflen@_;
		});
	};
	ast_add_child(ref size_node, size_value, global_arena);

	ast_add_child(ref result_node, data_node, global_arena);
	ast_add_child(ref result_node, size_node, global_arena);
	return cg_structinit(zero, context
	{
		.node := ref result_node;
		.local := ctx.local;
		.typecheck_only := ctx.typecheck_only;
	});
};

cg_builtin_curloc ::= func(ctx : context mut -> cgv)
{
	node ::= ctx.node;
	loc ::= node->loc;
	result_node : ast mut := deref node;
	result_node.children = zero;
	result_node.childcount = 0;
	result_node.type = ast_type.init;
	srcloc_tyname ::= "srcloc";
	result_node.init = ast_init
	{
		.name := srcloc_tyname;
		.name_len := zstrlen(srcloc_tyname);
	};

	filescope ::= scope_get_parent_filescope(ctx.local);
	
	file_node : ast mut := deref node;
	file_node.children = zero;
	file_node.childcount = 0;
	file_node.type = ast_type.init;
	file_node.init = ast_init
	{
		.name := "file";
		.name_len := zstrlen("file");
	};
	file_value ::= ast
	{
		.type := ast_type.expr;
		.expr := make_literal_expr(ast_literal_expr
		{
			.type := ast_literal_type.string_literal;
			.chars := filescope->scope_label;
			.chars_len := filescope->scope_label_len;
		});
	};
	ast_add_child(ref file_node, file_value, global_arena);

	line_node : ast mut := deref node;
	line_node.children = zero;
	line_node.childcount = 0;
	line_node.type = ast_type.init;
	line_node.init = ast_init
	{
		.name := "line";
		.name_len := zstrlen("line");
	};
	line_value ::= ast
	{
		.type := ast_type.expr;
		.expr := make_literal_expr(ast_literal_expr
		{
			.type := ast_literal_type.integral;
			.integral := loc.line@_;
		});
	};
	ast_add_child(ref line_node, line_value, global_arena);

	col_node : ast mut := deref node;
	col_node.children = zero;
	col_node.childcount = 0;
	col_node.type = ast_type.init;
	col_node.init = ast_init
	{
		.name := "column";
		.name_len := zstrlen("column");
	};
	col_value ::= ast
	{
		.type := ast_type.expr;
		.expr := make_literal_expr(ast_literal_expr
		{
			.type := ast_literal_type.integral;
			.integral := loc.column@_;
		});
	};
	ast_add_child(ref col_node, col_value, global_arena);

	ast_add_child(ref result_node, file_node, global_arena);
	ast_add_child(ref result_node, line_node, global_arena);
	ast_add_child(ref result_node, col_node, global_arena);

	return cg_structinit(zero, context
	{
		.node := ref result_node;
		.local := ctx.local;
		.typecheck_only := ctx.typecheck_only;
	});
};

cg_builtin_error ::= func(ctx : context mut -> cgv)
{
	node ::= ctx.node;
	msgnode ::= ast_get_child(node@_, 0);
	msg ::= sval_node(context
	{
		.node := msgnode;
		.local := ctx.local;
	});
	if((msg.tag) != (sval_tag.literal))
	{
		psyc_error_begin(node->loc);
		putzstr("Invalid call to builtin - param 0 must be a literal");
		psyc_diag_end();
		psyc_exit_failure();
	}
	lit ::= msg.literal;
	if((lit.type) != (ast_literal_type.string_literal))
	{
		psyc_error_begin(node->loc);
		putzstr("Invalid call to builtin - param 0 must be a string literal");
		psyc_diag_end();
		psyc_exit_failure();
	}
	psyc_colour_red();
	putzstr("custom");
	putzstr(" error ");
	print_srcloc(node->loc);
	putzstr(": ");
	putbytes(lit.chars, lit.chars_len);
	psyc_diag_end();
	psyc_exit_failure();
	return zero;
};

cg_builtin_atomic_rmw ::= func(ctx : context mut -> cgv)
{
	opnode ::= ast_get_child((ctx.node)@_, 0);
	ptrnode ::= ast_get_child((ctx.node)@_, 1);
	valnode ::= ast_get_child((ctx.node)@_, 2);
	ordernode ::= ast_get_child((ctx.node)@_, 3);

	op ::= sval_node(context
	{
		.node := opnode;
		.local := ctx.local;
		.typecheck_only := false;
	});

	ptr : cgv mut := cg_expr(context
	{
		.node := ptrnode;
		.local := ctx.local;
		.typecheck_only := false;
	});
	cgv_load_if_variable(ref ptr);
	ptrtype ::= ptr.type;
	if(!ty_is(ptrtype, tytag.ptr))
	{
		psyc_error_begin(ptrnode->loc);
		putzstr("parameter 1 to __atomic_rmw must be a pointer type. you have provided ");
		putchar('"');
		putty(ptrtype);
		putchar('"');
		psyc_diag_end();
		psyc_exit_failure();
	}
	ptr_uty ::= ty_deref(ptrtype);

	val : cgv mut := cg_expr(context
	{
		.node := valnode;
		.local := ctx.local;
		.typecheck_only := false;
	});
	cgv_load_if_variable(ref val);

	order ::= sval_node(context
	{
		.node := ordernode;
		.local := ctx.local;
		.typecheck_only := false;
	});
	// order is expected to be an integral literal
	// whose value directly corresponds to the LLVMAtomicOrdering to be passed to LLVMBuildAtomicRMW

	return cgv
	{
		.tag := cgvtag.misc;
		.ll := LLVMBuildAtomicRMW(ir, op.literal.integral, ptr.ll, val.ll, order.literal.integral, zero);
		.type := ptr_uty;
	};
};

cg_builtin_atomic_cmpxchg ::= func(ctx : context mut -> cgv)
{
	ptrnode ::= ast_get_child((ctx.node)@_, 0);
	cmpnode ::= ast_get_child((ctx.node)@_, 1);
	newnode ::= ast_get_child((ctx.node)@_, 2);
	ordernode ::= ast_get_child((ctx.node)@_, 3);

	ptr : cgv mut := cg_expr(context
	{
		.node := ptrnode;
		.local := ctx.local;
		.typecheck_only := false;
	});
	cgv_load_if_variable(ref ptr);
	ptrtype ::= ptr.type;
	if(!ty_is(ptrtype, tytag.ptr))
	{
		psyc_error_begin(ptrnode->loc);
		putzstr("parameter 0 to __atomic_cmpxchg must be a pointer type. you have provided ");
		putchar('"');
		putty(ptrtype);
		putchar('"');
		psyc_diag_end();
		psyc_exit_failure();
	}
	ptr_uty ::= ty_deref(ptrtype);

	cmp : cgv mut := cg_expr(context
	{
		.node := cmpnode;
		.local := ctx.local;
		.typecheck_only := false;
	});
	cgv_load_if_variable(ref cmp);

	new : cgv mut := cg_expr(context
	{
		.node := cmpnode;
		.local := ctx.local;
		.typecheck_only := false;
	});
	cgv_load_if_variable(ref new);

	order ::= sval_node(context
	{
		.node := ordernode;
		.local := ctx.local;
		.typecheck_only := false;
	});

	// order is expected to be an integral literal
	// whose value directly corresponds to the LLVMAtomicOrdering to be passed to LLVMBuildAtomicRMW

	xchg_result ::= LLVMBuildAtomicCmpXchg(ir, ptr.ll, cmp.ll, new.ll, order.literal.integral, _atomic_ordering.monotonic@s64, zero);
	return cgv
	{
		.tag := cgvtag.misc;
		.ll := LLVMBuildExtractValue(ir, xchg_result, 1, "success");
		.type := ty_getprim(primty.bool, zero);
	};
};
