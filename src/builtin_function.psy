builtin_assert_static ::= func(node : ast? -> v0)
{
	builtin_require_params(node, 1, 1);
	cond_sval ::= builtin_static_param(node, 0, tyid_bool_literal());
	cond ::= val_must_get_boolean(cond_sval, node);
	if(!cond)
	{
		psyc_error_begin(node->loc);
		putzstr("static assert failure: condition was false");
		putchar(10);
		ast_print_annotated_source(program_find_source_file(node->loc.file)->src, node, 1, underline_colour.red);
		psyc_diag_end();
		psyc_exit_failure();
	}
};

builtin_add_source_file_static ::= func(node : ast? -> v0)
{
	builtin_require_params(node, 1, 1);
	path_sval ::= builtin_static_param(node, 0, tyid_string_literal());
	
	path_strdata : u8 mut? mut;
	path_strlen : u64 mut;
	val_must_get_string(path_sval, node, ref path_strdata, ref path_strlen);
	path_zstr ::= sstr2zstr(path_strdata, path_strlen, string_arena);

	if(!file_exists(path_zstr))
	{
		psyc_error_begin(node->loc);
		putzstr("source file ");
		putchar('"');
		putzstr(path_zstr);
		putchar('"');
		putzstr(" could not be found:");
		putchar(10);
		ast_print_annotated_source(program_find_source_file(node->loc.file)->src, node, 1, underline_colour.red);
		psyc_diag_end();
		psyc_exit_failure();
	}
	program_queue_source_file(path_strdata, path_strlen);
};

builtin_add_build_file_static ::= func(node : ast? -> v0)
{
	builtin_require_params(node, 1, 2);
	path_sval ::= builtin_static_param(node, 0, tyid_string_literal());

	path_strdata : u8 mut? mut;
	path_strlen : u64 mut;
	val_must_get_string(path_sval, node, ref path_strdata, ref path_strlen);
	path_zstr ::= sstr2zstr(path_strdata, path_strlen, string_arena);

	config : u8? mut := "default";
	if(builtin_has_param(node, 1))
	{
		config_sval ::= builtin_static_param(node, 1, tyid_string_literal());
		config_strdata : u8 mut? mut;
		config_strlen : u64 mut;
		val_must_get_string(config_sval, node, ref config_strdata, ref config_strlen);
		config = sstr2zstr(config_strdata, config_strlen, string_arena);
	}
	if(!file_exists(path_zstr))
	{
		psyc_error_begin(node->loc);
		putzstr("source file ");
		putchar('"');
		putzstr(path_zstr);
		putchar('"');
		putzstr(" could not be found:");
		putchar(10);
		ast_print_annotated_source(program_find_source_file(node->loc.file)->src, node, 1, underline_colour.red);
		psyc_diag_end();
		psyc_exit_failure();
	}
	program_queue_build_file(path_strdata, path_strlen, config);
};

builtin_executable_static ::= func(node : ast? -> v0)
{
	builtin_require_params(node, 1, 1);
	name_sval ::= builtin_static_param(node, 0, tyid_string_literal());
	name_strdata : u8 mut? mut;
	name_strlen : u64 mut;
	val_must_get_string(name_sval, node, ref name_strdata, ref name_strlen);
	prog.output.name = sstr2zstr(name_strdata, name_strlen, string_arena);
};

builtin_optimization_static ::= func(node : ast? -> v0)
{
	builtin_require_params(node, 1, 1);
	opt_sval ::= builtin_static_param(node, 0, tyid_integral_literal());

	prog.optimization = (val_must_get_integer(opt_sval, node)@program_optimization);
};

builtin_debug_symbols_static ::= func(node : ast? -> v0)
{
	builtin_require_params(node, 1, 1);
	debug_symbols_sval ::= builtin_static_param(node, 0, tyid_bool_literal());

	prog.debug_symbols = val_must_get_boolean(debug_symbols_sval, node);
};

