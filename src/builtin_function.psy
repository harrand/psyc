builtin_assert_static ::= func(node : ast? -> v0)
{
	builtin_require_params(node, 1, 1);
	cond_sval ::= builtin_static_param(node, 0, tyid_bool_literal());
	if(!(cond_sval.primitive.boolean))
	{
		psyc_error_begin(node->loc);
		putzstr("ASSERT FAIL");
		psyc_diag_end();
		psyc_exit_failure();
	}
};

builtin_add_source_file_static ::= func(node : ast? -> v0)
{
	builtin_require_params(node, 1, 1);
	path_sval ::= builtin_static_param(node, 0, tyid_string_literal());
	path_zstr ::= sstr2zstr(path_sval.primitive.chars, path_sval.primitive.chars_len, string_arena);

	if(!file_exists(path_zstr))
	{
		psyc_error_begin(node->loc);
		putzstr("source file ");
		putchar('"');
		putzstr(path_zstr);
		putchar('"');
		putzstr(" could not be found:");
		putchar(10);
		ast_print_annotated_source(program_find_source_file(node->loc.file)->src, node, 1);
		psyc_diag_end();
		psyc_exit_failure();
	}
	program_queue_source_file(path_sval.primitive.chars, path_sval.primitive.chars_len);
};

builtin_add_build_file_static ::= func(node : ast? -> v0)
{
	builtin_require_params(node, 1, 2);
	path_sval ::= builtin_static_param(node, 0, tyid_string_literal());
	path_zstr ::= sstr2zstr(path_sval.primitive.chars, path_sval.primitive.chars_len, string_arena);

	config : u8? mut := "default";
	if(builtin_has_param(node, 1))
	{
		config_sval ::= builtin_static_param(node, 1, tyid_string_literal());
		config = sstr2zstr(config_sval.primitive.chars, config_sval.primitive.chars_len, string_arena);
	}
	if(!file_exists(path_zstr))
	{
		psyc_error_begin(node->loc);
		putzstr("source file ");
		putchar('"');
		putzstr(path_zstr);
		putchar('"');
		putzstr(" could not be found:");
		putchar(10);
		ast_print_annotated_source(program_find_source_file(node->loc.file)->src, node, 1);
		psyc_diag_end();
		psyc_exit_failure();
	}
	program_queue_build_file(path_sval.primitive.chars, path_sval.primitive.chars_len, config);
};

builtin_executable_static ::= func(node : ast? -> v0)
{
	builtin_require_params(node, 1, 1);
	name_sval ::= sval_convert_to(builtin_fn_param(node, 0), tyid_string_literal(), node);
	name ::= sstr2zstr(name_sval.primitive.chars, name_sval.primitive.chars_len, string_arena);

	prog.output.name = name;
};

builtin_optimization_static ::= func(node : ast? -> v0)
{
	builtin_require_params(node, 1, 1);
	opt_sval ::= sval_convert_to(builtin_fn_param(node, 0), tyid_integral_literal(), node);

	prog.optimization = (opt_sval.primitive.integer@program_optimization);
};

builtin_debug_symbols_static ::= func(node : ast? -> v0)
{
	builtin_require_params(node, 1, 1);
	debug_symbols_sval ::= sval_convert_to(builtin_fn_param(node, 0), tyid_bool_literal(), node);

	prog.debug_symbols = (debug_symbols_sval.primitive.boolean);
};

