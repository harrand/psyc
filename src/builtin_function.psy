builtin_assert_static ::= func(node : ast? -> v0)
{
	builtin_require_params(node, 1, 1);
	par0ty ::= ty_set_qual(tyid.bool, tyqual.qual_static);
	cond_sval ::= sval_convert_to(builtin_fn_param(node, 0), par0ty, node);
	if(!(cond_sval.primitive.boolean))
	{
		psyc_error_begin(node->loc);
		putzstr("ASSERT FAIL");
		psyc_diag_end();
		psyc_exit_failure();
	}
};

builtin_add_source_file_static ::= func(node : ast? -> v0)
{
	builtin_require_params(node, 1, 1);
	par0ty ::= tyid_string_literal();
	path_sval ::= sval_convert_to(builtin_fn_param(node, 0), par0ty, node);
	path_zstr ::= sstr2zstr(path_sval.primitive.chars, path_sval.primitive.chars_len, string_arena);

	if(!file_exists(path_zstr))
	{
		psyc_error_begin(node->loc);
		putzstr("source file ");
		putchar('"');
		putzstr(path_zstr);
		putchar('"');
		putzstr(" could not be found:");
		putchar(10);
		ast_print_annotated_source(program_find_source_file(node->loc.file)->src, node, 1);
		psyc_diag_end();
		psyc_exit_failure();
	}
	program_queue_source_file(path_sval.primitive.chars, path_sval.primitive.chars_len);
};

builtin_add_build_file_static ::= func(node : ast? -> v0)
{
	builtin_require_params(node, 1, 2);
	par0ty ::= tyid_string_literal();
	path_sval ::= sval_convert_to(builtin_fn_param(node, 0), par0ty, node);
	path_zstr ::= sstr2zstr(path_sval.primitive.chars, path_sval.primitive.chars_len, string_arena);

	par1ty ::= tyid_string_literal();
	config : u8? mut := "default";
	config_sval : sval mut := builtin_fn_param(node, 1);
	if(sval_is_valid(config_sval))
	{
		config_sval = sval_convert_to(config_sval, par1ty, node);
		config = sstr2zstr(config_sval.primitive.chars, config_sval.primitive.chars_len, string_arena);
	}
	if(!file_exists(path_zstr))
	{
		psyc_error_begin(node->loc);
		putzstr("source file ");
		putchar('"');
		putzstr(path_zstr);
		putchar('"');
		putzstr(" could not be found:");
		putchar(10);
		ast_print_annotated_source(program_find_source_file(node->loc.file)->src, node, 1);
		psyc_diag_end();
		psyc_exit_failure();
	}
	program_queue_build_file(path_sval.primitive.chars, path_sval.primitive.chars_len, config);
};

builtin_executable_static ::= func(node : ast? -> v0)
{
	builtin_require_params(node, 1, 1);
	par0ty ::= tyid_string_literal();
	name_sval ::= sval_convert_to(builtin_fn_param(node, 0), par0ty, node);
	name ::= sstr2zstr(name_sval.primitive.chars, name_sval.primitive.chars_len, string_arena);
	prog.output.name = name;
};

