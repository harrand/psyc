// preamble
ir : u64 mut;

cg_helper_functions ::= struct
{
	children : func(ctx : context mut, prog : program mut&, offset : u64, back_offset : u64) -> v0;
	type : func(t : ty, prog : program mut&) -> u64;
};
cg : cg_helper_functions mut;

// cgv code begins here

cgvtag ::= enum
{
	.misc := 1;
	.var := 2;
};

cgv ::= struct
{
	tag : cgvtag;
	ll : u64;
	sv : sval;
};

cgv_ty ::= func(v : cgv) -> ty
{
	sv ::= v.sv;
	return sv.type;
};

cgv_has_sv ::= func(v : cgv) -> bool
{
	return sval_hasval(v.sv);
};

cgv_is_null ::= func(v : cgv) -> bool
{
	return sval_isnull(v.sv);
};

cgv_is_zero ::= func(v : cgv) -> bool
{
	return sval_iszero(v.sv);
};

cgv_conv_fn ::= struct
{
	fn : func(v : cgv, to : ty, prog : program mut&, loc : srcloc) -> cgv;
};
cgv_conv_table : cgv_conv_fn mut#16;

cgv_zero_as ::= func(type : ty, prog : program mut&) -> cgv
{
	ret : cgv mut := cgv
	{
		.tag := cgvtag.misc;
		.sv := sval
		{
			.type := type;
			.tag := sval_tag.val_zero;
		};
	};
	if((type.tag) == (tytag.ptr))
	{
		ptr ::= type.ptr;
		underlying ::= deref((ptr.underlying)@ty&);
		cg_u ::= cg.type(underlying, prog);
		(ret.ll) = LLVMConstPointerNull(cg_u);
		return ret;
	}
	if((type.tag) == (tytag.arr))
	{
		psyc_panic(zero, "todo: zero_as array");
	}
	cg_t ::= cg.type(type, prog);
	(ret.ll) = LLVMConstNull(cg_t);
	return ret;
};

cgv_convert ::= func(v : cgv, to : ty, prog : program mut&, loc : srcloc) -> cgv
{
	if(cgv_is_zero(v))
	{
		return cgv_zero_as(to, prog);
	}
	sv ::= v.sv;
	from ::= sv.type;
	conv ::= ty_get_conv(from, to);

	do_conv ::= deref(cgv_conv_table at (conv@s64));
	ret : cgv mut := do_conv.fn(v, to, prog, loc);
	retsv ::= ref(ret.sv);
	(retsv->type) = to;
	return ret;
};

cgv_load ::= func(v : cgv mut, prog : program mut&) -> cgv
{
	(v.ll) = LLVMBuildLoad2(ir, cg.type(cgv_ty(v), prog), v.ll, "");
	return v;
};

cgv_load_if_variable ::= func(v : cgv mut&, prog : program mut&) -> v0
{
	if((v->tag) == (cgvtag.var))
	{
		deref(v) = cgv_load(deref v, prog);
	}
};

== build ==
{
	add_source_file("sval.psy");
	add_source_file("llvm.psy");
}
