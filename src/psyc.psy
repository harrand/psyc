setup_arenas ::= macro() -> v0
{
	permanent_arena_val : arena mut := arena_create(4096 * 1024);
	permanent_arena ::= ref permanent_arena_val;
	defer puts("arena free: ");
	defer putuint(permanent_arena_val.cursor);
	defer puts("B");
	defer putchar(10);
	defer arena_destroy(permanent_arena_val);

	frame_arena_val : arena mut := arena_create(4096 * 1024);
	frame_arena ::= ref frame_arena_val;
	defer arena_destroy(frame_arena_val);
};

// read a file path. allocates for the whole file data. returns null if file didnt exist.
fully_read_file ::= func(path : u8&, a : arena mut&) -> u8 mut&
{
	len ::= file_size_bytes(path);
	if(len == 0)
	{
		return null;
	}
	data : u8 mut& := arena_push(a, len + 1);
	file_read(path, data, len);
	deref(data at len) = 0;
	return data;
};

main ::= func() -> s32
{
	setup_arenas();

	lex_setup();
	grammar_setup(permanent_arena);
	type_setup(permanent_arena);
	semal_setup(permanent_arena);

	prog : program mut := zero;
	args ::= ref(prog.compile_args);
	deref(args) = psyc_parse_args(permanent_arena);
	if(args->verbose_parse)
	{
		puts("grammar size: ");
		putuint(grammar_install_count);
		putchar(10);
	}
	defer psyc_report_timings();

	if(!file_exists(args->build_file))
	{
		psyc_error_begin(zero);
		puts("build file ");
		putchar('"');
		puts(args->build_file);
		putchar('"');
		puts(" could not be located");
		psyc_diag_end();
		psyc_exit_failure();
	}
	psyc_message_begin(zero);
	puts("building file ");
	putchar('"');
	puts(args->build_file);
	putchar('"');
	psyc_diag_end();

	buildfile_data ::= fully_read_file(args->build_file, permanent_arena);

	lstate ::= lex(args->build_file, buildfile_data, permanent_arena, args->verbose_lex);
	pstate ::= parse(ref lstate, args->verbose_parse);

	semal_file(ref prog, ref (pstate.root));
	return 0;
};

== build ==
{
	add_source_file("args.psy");
	add_source_file("stdlib/file.psy");
	add_source_file("diag.psy");

	add_source_file("stdlib/hash.psy");
	add_source_file("grammar.psy");

	add_source_file("ast.psy");
	add_source_file("parse.psy");

	add_source_file("program.psy");
	add_source_file("semal.psy");

	set_output_directory("build");
	set_executable("psyc");

	add_link_library("Kernel32.lib");
	add_link_library("Shell32.lib");
	add_link_library("libcmt.lib");
}
