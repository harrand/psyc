setup_arenas ::= macro() -> v0
{
	permanent_arena_val : arena mut := arena_create(4096 * 1024);
	permanent_arena ::= ref permanent_arena_val;
	defer puts("arena free: ");
	defer putuint(permanent_arena_val.cursor);
	defer puts("B");
	defer putchar(10);
	defer arena_destroy(permanent_arena_val);

	frame_arena_val : arena mut := arena_create(4096 * 1024);
	frame_arena ::= ref frame_arena_val;
	defer arena_destroy(frame_arena_val);
};

// read a file path. allocates for the whole file data. returns null if file didnt exist.
fully_read_file ::= func(path : u8&, a : arena mut&) -> u8 mut&
{
	len ::= file_size_bytes(path);
	if(len == 0)
	{
		return null;
	}
	data : u8 mut& := arena_push(a, len + 1);
	file_read(path, data, len);
	deref(data at len) = 0;
	return data;
};

compile_added_source ::= func(prog : program mut&, path : u8&, source : u8&, a : arena mut&) -> v0
{
	args ::= ref(prog->compile_args);
	l ::= lex(path, source, a, args->verbose_lex);
	p ::= parse(ref l, args->verbose_parse);
	semal_file(prog, ref(p.root));
};

compile_added_file ::= func(prog : program mut&, source_file : u8&, a : arena mut&) -> v0
{
	compile_added_source(prog, source_file, fully_read_file(source_file, a), a);
};

compile_program ::= func(prog : program mut&, a : arena mut&) -> v0
{
	args ::= ref(prog->compile_args);
	compile_added_file(prog, args->build_file, a);
};

main ::= func() -> s32
{
	setup_arenas();

	lex_setup();
	grammar_setup(permanent_arena);
	type_setup(permanent_arena);
	semal_setup(permanent_arena);

	prog : program mut := zero;
	args ::= ref(prog.compile_args);
	deref(args) = psyc_parse_args(permanent_arena);
	if(args->verbose_parse)
	{
		puts("grammar size: ");
		putuint(grammar_install_count);
		putchar(10);
	}
	defer psyc_report_timings();

	if(!file_exists(args->build_file))
	{
		psyc_error_begin(zero);
		puts("build file ");
		putchar('"');
		puts(args->build_file);
		putchar('"');
		puts(" could not be located");
		psyc_diag_end();
		psyc_exit_failure();
	}
	compile_program(ref prog, permanent_arena);
	return 0;
};

== build ==
{
	add_source_file("args.psy");
	add_source_file("stdlib/file.psy");
	add_source_file("diag.psy");

	add_source_file("stdlib/hash.psy");
	add_source_file("grammar.psy");

	add_source_file("ast.psy");
	add_source_file("parse.psy");

	add_source_file("program.psy");
	add_source_file("semal.psy");

	set_output_directory("build");
	set_executable("psyc");

	add_link_library("Kernel32.lib");
	add_link_library("Shell32.lib");
	add_link_library("libcmt.lib");
}
