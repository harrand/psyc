append_link_library_string ::= func(str : u8 mut? mut? -> v0)
{
	oldstr ::= deref str;
	if((prog.link_libraries_count) == 0)
	{
		return;
	}
	// figure out how much space we need
	space_needed : u64 mut := 0;
	i : u64 mut;
	for(i = 0, i < (prog.link_libraries_count), i = i + 1)
	{
		space_needed = (space_needed + (deref((prog.link_libraries_lens) # i)) + 1);
	}
	oldlen ::= zstrlen(oldstr);
	newstr : u8 mut? := arena_alloc(global_arena, space_needed + oldlen);
	memcopy(newstr, oldstr, oldlen);
	// turn the zero terminator in the old string to a space
	deref(newstr # oldlen) = ' ';
	offset : u64 mut := 0;
	for(i = 0, i < (prog.link_libraries_count), i = i + 1)
	{
		curstr ::= deref((prog.link_libraries) # i);
		curlen ::= deref((prog.link_libraries_lens) # i);
		memcopy(newstr # (oldlen + offset), curstr, curlen);
		deref(newstr # (oldlen + offset + curlen)) = ' ';
		offset = (offset + curlen + 1);
	}
	deref(newstr # (space_needed + oldlen - 1)) = 0;
	deref(str) = newstr;
};

append_msvc_flags ::= func(str : u8 mut? mut? -> v0)
{
	oldstr ::= deref str;
	oldlen ::= zstrlen(oldstr);

	msvc_flags ::= " /NODEFAULTLIB /SUBSYSTEM:CONSOLE /ENTRY:_psymain /STACK:8000000,8000000 Kernel32.lib Shell32.lib";
	space_needed ::= zstrlen(msvc_flags);

	newstr : u8 mut? := arena_alloc(global_arena, space_needed + oldlen + 1);
	memcopy(newstr, oldstr, oldlen);
	memcopy(newstr # oldlen, msvc_flags, zstrlen(msvc_flags));
	deref(newstr # (space_needed + oldlen)) = 0;
	deref(str) = newstr;
};

append_linux_gnu_flags ::= func(str : u8 mut? mut? -> v0)
{
	oldstr ::= deref str;
	oldlen ::= zstrlen(oldstr);

	gnu_flags ::= " -z stack-size=8000000";
	space_needed ::= zstrlen(gnu_flags);

	newstr : u8 mut? := arena_alloc(global_arena, space_needed + oldlen + 1);
	memcopy(newstr, oldstr, oldlen);
	memcopy(newstr # oldlen, gnu_flags, zstrlen(gnu_flags));
	deref(newstr # (space_needed + oldlen)) = 0;
	deref(str) = newstr;
};

append_output ::= func(str : u8 mut? mut? -> v0)
{
	oldstr ::= deref str;
	oldlen ::= zstrlen(oldstr);

	static if(_win32)
	{
		out_flags : u8? mut := " /OUT:";
		if(prog.debug_symbols)
		{
			out_flags = " /DEBUG /OUT:";
		}
	}
	else
	{
		out_flags ::= " -o ";
	}
	out_name ::= prog.output.name;
	extension ::= default_exe_extension;
	space_needed ::= zstrlen(out_flags) + zstrlen(prog.output.name) + zstrlen(extension);

	newstr : u8 mut? := arena_alloc(global_arena, space_needed + oldlen + 1);
	memcopy(newstr, oldstr, oldlen);
	memcopy(newstr # oldlen, out_flags, zstrlen(out_flags));
	memcopy(newstr # (oldlen + zstrlen(out_flags)), prog.output.name, zstrlen(prog.output.name));
	memcopy(newstr # (oldlen + zstrlen(out_flags) + zstrlen(prog.output.name)), extension, zstrlen(extension));
	deref(newstr # (space_needed + oldlen)) = 0;
	deref(str) = newstr;
};

prepend_linker_name ::= func(str : u8 mut? mut? -> v0)
{
	oldstr ::= deref str;
	oldlen ::= zstrlen(oldstr);

	static if(_win32)
	{
		lnkname ::= "lld-link ";
	}
	else
	{
		lnkname ::= "ld ";
	}
	space_needed ::= zstrlen(lnkname);

	newstr : u8 mut? := arena_alloc(global_arena, space_needed + oldlen + 1);
	memcopy(newstr, lnkname, space_needed);
	memcopy(newstr # space_needed, oldstr, oldlen);
	deref(newstr # (space_needed + oldlen)) = 0;
	deref(str) = newstr;
};

prepend_archiver_name ::= func(str : u8 mut? mut? -> v0)
{
	oldstr ::= deref str;
	oldlen ::= zstrlen(oldstr);

	static if(_win32)
	{
		lnkname ::= "llvm-lib ";
	}
	else
	{
		lnkname ::= "ar rcs ";
	}
	space_needed ::= zstrlen(lnkname);

	newstr : u8 mut? := arena_alloc(global_arena, space_needed + oldlen + 1);
	memcopy(newstr, lnkname, space_needed);
	memcopy(newstr # space_needed, oldstr, oldlen);
	deref(newstr # (space_needed + oldlen)) = 0;
	deref(str) = newstr;
};

link ::= func( -> v0)
{
	if((prog.type) == (program_type.object))
	{
		return;
	}
	psyc_timed_begin(psyc_stage.link);	
	defer psyc_timed_end();

	path_len ::= zstrlen(prog.output.name) + zstrlen(default_obj_extension);
	cmd : u8 mut? mut := arena_alloc(global_arena, path_len + 2);
	memcopy(cmd # 0, prog.output.name, zstrlen(prog.output.name));
	memcopy(cmd # zstrlen(prog.output.name), default_obj_extension, zstrlen(default_obj_extension));
	deref(cmd # path_len) = ' ';
	deref(cmd # (path_len + 1)) = 0;

	if((prog.type) == (program_type.executable))
	{
		append_link_library_string(ref cmd);
		static if(_win32)
		{
			append_msvc_flags(ref cmd);
		}
		else
		{
			append_linux_gnu_flags(ref cmd);
		}
		prepend_linker_name(ref cmd);
		append_output(ref cmd);
	}

	if((prog.type) == (program_type.library))
	{
		prepend_archiver_name(ref cmd);
	}

	args ::= ref(prog.compile_args);
	if(args->verbose_link)
	{
		putzstr("Linker Command: ");
		putchar(10);
		putzstr(cmd);
		putchar(10);
	}

	retcode ::= sys_shell_command(cmd, zstrlen(cmd), envp, global_arena);
	if(retcode != zero)
	{
		psyc_error_begin(zero);
		putzstr("Linker returned exit code ");
		putuint(retcode);
		putzstr(".");
		psyc_diag_end();
		psyc_exit_failure();
	}
};
