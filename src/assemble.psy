assemble : func( -> v0)
{
	psyc_timed_begin(psyc_stage.assembly);

	LLVMInitializeX86AsmParser();
	LLVMInitializeX86AsmPrinter();
	LLVMInitializeX86TargetInfo();
	LLVMInitializeX86Target();
	LLVMInitializeX86Disassembler();
	LLVMInitializeX86TargetMC();

	triple ::= LLVMGetDefaultTargetTriple();
	target : u64 mut;
	error : u8? mut := zero;

	if(LLVMGetTargetFromTriple(triple, ref target, ref error) != 0)
	{
		psyc_error_begin(zero);
		putzstr("Error retrieving LLVM target from triple ");
		putchar('"');
		putzstr(triple);
		putchar('"');
		putzstr(": ");
		putzstr(error);
		// LLVMDisposeMessage(error);
		psyc_diag_end();
		psyc_exit_failure();
	}

	// these represent backend codegen flags (e.g asm instructions used)
	// they arent the same as O0-O3 but both offer increasing runtime perf
	// for that reason i will directly re-use prog.opt
	//codegen_level_none ::= 0;
	//codegen_level_less ::= 1;
	//codegen_level_default ::= 2;
	//codegen_level_aggressive ::= 3;
	optval ::= (prog.optimization)@s64@s32;
	LLVMRelocPIC ::= 2;
	target_machine ::= LLVMCreateTargetMachine(target, triple, "generic", "", optval, LLVMRelocPIC, 0);

	LLVMSetTarget(prog.cg, triple);

	// optimisation passes
	options ::= LLVMCreatePassBuilderOptions();

	opt_levels : u8? mut[4];
	[opt_levels # 0] = "default<O0>";
	[opt_levels # 1] = "default<O1>";
	[opt_levels # 2] = "default<O2>";
	[opt_levels # 3] = "default<O3>";

	if(LLVMRunPasses(prog.cg, [opt_levels # optval], target_machine, options) != 0)
	{
		psyc_error_begin(zero);
		putzstr("Unknown error running LLVM passes");
		psyc_diag_end();
		psyc_exit_failure();
	}

	args ::= ref(prog.compile_args);

	directory_create(prog.output.dir);
	directory_set_cwdz(prog.output.dir);
	path_len ::= zstrlen(prog.output.name) + zstrlen(default_obj_extension);
	path : u8 mut? := arena_alloc(global_arena, path_len + 1);
	memcopy(path # 0, prog.output.name, zstrlen(prog.output.name));
	memcopy(path # zstrlen(prog.output.name), default_obj_extension, zstrlen(default_obj_extension));
	[path # path_len] = 0;

	errmsg : u8? mut;
	if(LLVMTargetMachineEmitToFile(target_machine, prog.cg, path, 1, ref errmsg) != 0)
	{
		psyc_error_begin(zero);
		putzstr("Error while generating assembly: ");
		putzstr(errmsg);
		psyc_diag_end();
		psyc_exit_failure();
	}
	psyc_timed_end();
};
