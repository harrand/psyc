assemble ::= func(prog : program? -> v0)
{
	psyc_timed_begin(psyc_stage.assembly);

	dbg_setup_codeview(prog);

	defer psyc_timed_end();
	LLVMInitializeX86AsmParser();
	LLVMInitializeX86AsmPrinter();
	LLVMInitializeX86TargetInfo();
	LLVMInitializeX86Target();
	LLVMInitializeX86Disassembler();
	LLVMInitializeX86TargetMC();

	triple ::= LLVMGetDefaultTargetTriple();
	target : u64 mut;
	error : u8? mut := zero;

	if(LLVMGetTargetFromTriple(triple, ref target, ref error) != 0)
	{
		psyc_error_begin(zero);
		putzstr("Error retrieving LLVM target from triple ");
		putchar('"');
		putzstr(triple);
		putchar('"');
		putzstr(": ");
		putzstr(error);
		// LLVMDisposeMessage(error);
		psyc_diag_end();
		psyc_exit_failure();
	}

	// these represent backend codegen flags (e.g asm instructions used)
	// they arent the same as O0-O3 but both offer increasing runtime perf
	// for that reason i will directly re-use prog->opt
	//codegen_level_none ::= 0;
	//codegen_level_less ::= 1;
	//codegen_level_default ::= 2;
	//codegen_level_aggressive ::= 3;
	optval ::= (prog->opt)@s64@s32;
	target_machine ::= LLVMCreateTargetMachine(target, triple, "generic", "", optval, 0, 0);
	LLVMSetTarget(prog->codegen, triple);

	// optimisation passes
	options ::= LLVMCreatePassBuilderOptions();

	opt_levels : u8? mut[4];
	deref(opt_levels # 0) = "default<O0>";
	deref(opt_levels # 1) = "default<O1>";
	deref(opt_levels # 2) = "default<O2>";
	deref(opt_levels # 3) = "default<O3>";

	if(LLVMRunPasses(prog->codegen, deref(opt_levels # optval), target_machine, options) != 0)
	{
		psyc_error_begin(zero);
		putzstr("Unknown error running LLVM passes");
		psyc_diag_end();
		psyc_exit_failure();
	}

	args ::= ref(prog->compile_args);
	if(args->verbose_codegen)
	{
		str ::= LLVMPrintModuleToString(mod);
		putzstr("Codegen: ");
		putchar(10);
		putzstr(str);
		putchar(10);
	}

	directory_set_cwdz(args->output_dir);
	path_len ::= (prog->name_len) + zstrlen(default_obj_extension);
	path : u8 mut? := arena_alloc(global_arena, path_len + 1);
	memcopy(path # 0, prog->name, prog->name_len);
	memcopy(path # (prog->name_len), default_obj_extension, zstrlen(default_obj_extension));
	deref(path # path_len) = 0;

	errmsg : u8? mut;
	if(LLVMTargetMachineEmitToFile(target_machine, prog->codegen, path, 1, ref errmsg) != 0)
	{
		psyc_error_begin(zero);
		putzstr("Error while generating assembly: ");
		putzstr(errmsg);
		psyc_diag_end();
		psyc_exit_failure();
	}
};

== build ==
{
	add_source_file("program.psy");
	add_source_file("build.psy");
}
