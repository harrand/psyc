ast ::= struct
{
	// top-level bits
	tag : ast_tag;
	utok : ast_unparsed_token;
	stmt : ast_stmt_tag;
	expr : ast_expr_tag;

	// expr payloads
	literal : ast_literal;
	symbol : ast_symbol;
	unop : ast_unop;
	biop : ast_biop;
	callfunc : ast_callfunc;
	blkinit : ast_blkinit;

	// other payloads
	decl : ast_decl;
	typename : ast_typename;
	init : ast_init;
	structure : ast_structure;
	function : ast_function;
	enumeration : ast_enumeration;
	assembly : ast_assembly;

	// meta info
	begin_cursor : u64;
	end_cursor : u64;
	loc : srcloc;
	children : v0?;
	children_count : u64;
	children_cap : u64;
};

ast_tag ::= enum
{
	.unparsed_token := 1;
	.expr := 2;
	.stmt := 3;
	.decl := 4;
	.typename := 5;
	.init := 6;
	.structure := 7;
	.function := 8;
	.enumeration := 9;
	.assembly := 10;
};

ast_unparsed_token ::= struct
{
	tok : token_data;
};

ast_stmt_tag ::= enum
{
	.expr := 1;
	.decl := 2;
	.structure := 3;
	.function := 4;
	.enumeration := 5;
	.assembly := 6;
	.if_block := 7;
	.while_block := 8;
	.for_block := 9;
};

ast_expr_tag ::= enum
{
	.symbol := 1;
	.literal := 2;
	.zero_value := 3;
	.unop := 4;
	.biop := 5;
	.callfunc := 6;
	.blkinit := 7;
	.dereference := 8;
};

ast_decl_tag ::= enum
{
	.await_type := 1;
	.noinit := 2;
	.await_init_expr := 3;
	.finalised := 4;
	// todo: intermediate parser state we can generate unique hashes from
};

ast_decl ::= struct
{
	tag : ast_decl_tag;
	name : u8?;
	name_len : u64;
	// note: first child of a decl is always the typename
	// the second child is the init (if there is one)
};

ast_typename_descriptor ::= enum
{
	.base := 1;
	.array := 2;
	.pointer := 3;
	.function := 4;
	.deduced := 5;
};

ast_typename_tag ::= enum
{
	.wip := 1;
	.array_await_length := 2;
	.function_await_param_or_arrow := 3;
	.function_await_return_type := 4;
	.finalised := 5;
};

ast_typename ::= struct
{
	tag : ast_typename_tag;

	quals : tyqual;
	descriptor : ast_typename_descriptor;
	basename : u8?;
	basename_len : u64;
	// pointer means child 0 is the underlying type
	// array means child 0 is the underlying type and child 1 is the array length
	// function means child 0-(n-1) are the param typenames and child n is the return typename
};

ast_literal_tag ::= enum
{
	.integer := 1;
	.floating_point := 2;
	.char := 3;
	.string := 4;
	.boolean := 5;
};

ast_literal ::= struct
{
	tag : ast_literal_tag;
	integer : s64;
	floating_point : f64;
	// both for char and string literal (escaped char literals have more than 1 char in source for example)
	chars : u8?;
	chars_len : u64;
	boolean : bool;
};

ast_symbol ::= struct
{
	data : u8?;
	len : u64;
};

ast_unop ::= enum
{
	.minus := 1;
	.bitwise_invert := 2;
	.logical_invert := 3;
	.reference := 4;
};
ast_unop_tokens : lex_token mut[5] mut := zero;

ast_biop ::= enum
{
	.assign := 1;
	.compare_equal := 2;
	.compare_not_equal := 3;
	.compare_less := 4;
	.compare_less_equal := 5;
	.compare_greater := 6;
	.compare_greater_equal := 7;
	.add := 8;
	.sub := 9;
	.mul := 10;
	.div := 11;
	.bitwise_or := 12;
	.bitwise_and := 13;
	.logical_or := 14;
	.logical_and := 15;
	.xor := 16;
	.bitshift_left := 17;
	.bitshift_right := 18;
	.modulo := 19;
	.at := 20;
	.field := 21;
	.ptr_field := 22;
	.cast := 23;
};
ast_biop_tokens : lex_token mut[24] mut := zero;

ast_all_op_tokens : lex_token mut[29] mut := zero;
ast_all_op_tokens_cursor : u64 mut := 0;

ast_callfunc_tag ::= enum
{
	// todo: intermediate parser state we can generate unique hashes from
};

ast_callfunc ::= struct
{
	tag : ast_callfunc_tag;
	call_name : u8?;
	call_name_len : u64;
};

ast_blkinit ::= struct
{
	typename : ast_typename;
};

ast_init ::= struct
{
	name : u8?;
	name_len : u64;
};

ast_structure_tag ::= enum
{
	.wip := 1;
	.finalised := 2;
};

ast_structure ::= struct
{
	tag : ast_structure_tag;
	name : u8?;
	name_len : u64;
};

ast_function_tag ::= enum
{
};

ast_function ::= struct
{
	tag : ast_function_tag;
	name : u8?;
	name_len : u64;
	return_type : ast_typename;
};

ast_enumeration_tag ::= enum
{
	
};

ast_enumeration ::= struct
{
	tag : ast_enumeration_tag;
	name : u8?;
	name_len : u64;
};

ast_assembly_tag ::= enum
{
};

ast_assembly ::= struct
{
	tag : ast_assembly_tag;
	name : u8?;
	name_len : u64;
	return_type : ast_typename;
};

ast_reserve_children ::= func(node : ast mut?, count : u64, ar : arena mut? -> v0)
{
	if(node->children_cap < count)
	{
		old ::= node->children;
		oldcap ::= node->children_cap;
		node->children_cap = count;
		node->children = arena_alloc(ar, __sizeof(ast) * (node->children_cap));	
		if(old != zero)
		{
			memcopy(node->children, old, oldcap * __sizeof(ast));
			arena_free(ar, old, oldcap * __sizeof(ast));
		}
	}
};

ast_add_child ::= func(node : ast mut?, child : ast, ar : arena mut? -> v0)
{
	if(node->children == zero)
	{
		node->children_cap = 2;
		node->children = arena_alloc(ar, __sizeof(ast) * (node->children_cap));
	}
	while(node->children_cap <= (node->children_count))
	{
		old ::= node->children;
		oldcap ::= node->children_cap;

		node->children_cap = (node->children_cap * 2);
		node->children = arena_alloc(ar, __sizeof(ast) * (node->children_cap));	
		memcopy(node->children, old, oldcap * __sizeof(ast));
		arena_free(ar, old, oldcap * __sizeof(ast));
	}
	children ::= node->children@ast mut?;
	deref(children # (node->children_count)) = child;
	node->children_count = (node->children_count + 1);
	// note: i have decided that if you add a node as a child, the parent node almost certainly should include it when quoting source
	// so im potentially extending its end cursor
	// if thats wrong then comment this shit out.
	if(node->end_cursor < (child.end_cursor))
	{
		node->end_cursor = (child.end_cursor);
	}
};

ast_get_child ::= func(node : ast mut?, idx : u64 -> ast mut?)
{
	return node->children@ast mut? # idx;
};

// countof + 1 because enum entries start at 1 not 0
tag_offset_utok ::= func(-> u64 weak)
{
	return __countof(ast_tag) + 1;
};

tag_offset_expr ::= func(-> u64 weak)
{
	return tag_offset_utok() + __countof(lex_token) + 1;
};

tag_offset_stmt ::= func(-> u64 weak)
{
	return tag_offset_expr() + __countof(ast_expr_tag) + 1;
};

tag_offset_typename ::= func(-> u64 weak)
{
	return tag_offset_stmt() + __countof(ast_stmt_tag) + 1;
};

tag_offset_function ::= func(-> u64 weak)
{
	return tag_offset_typename() + __countof(ast_typename_tag) + 1;
};

tag_offset_structure ::= func(-> u64 weak)
{
	return tag_offset_function() + __countof(ast_function_tag) + 1;
};

tag_offset_enumeration ::= func(-> u64 weak)
{
	return tag_offset_structure() + __countof(ast_structure_tag) + 1;
};

tag_offset_assembly ::= func(-> u64 weak)
{
	return tag_offset_enumeration() + __countof(ast_enumeration_tag) + 1;
};

tag_offset_decl ::= func(-> u64 weak)
{
	return tag_offset_assembly() + __countof(ast_assembly_tag) + 1;
};


// whats the largest possible value we can get from ast_uid?
ast_maximum_uid ::= func(-> u64)
{
	return (tag_offset_decl() + __countof(ast_decl_tag) + 1);
};

ast_uid ::= func(node : ast? -> u64)
{
	// the idea is this generates a unique id for every possible specific ast type
	// some are more strict than others (e.g all asts that are functions will have the same uid, but asts that are decls will have a different uid depending on the decl tag)
	// the default is that all ast_tag N have the same uid. the exceptions to that are below
	// the reason for this is the grammar might have a reduction for a literal expression but not necessarily a symbol expression. but the grammar might treat all functions/structs the exact same.
	// the previous thing was to try and hash everything together but it was slow (both for all the hash calculations and dealing with collisions)
	// this way is much more specific and attempts to guarantee uniqueness in cases where it matters without a noticeable perf cost.
	tag ::= node->tag;
	if(tag == zero){return zero;}
	if(tag == (ast_tag.unparsed_token))
	{
		return tag_offset_utok() + (node->utok.tok.tok@s64);
	}
	if(tag == (ast_tag.expr))
	{
		return tag_offset_expr() + (node->expr@s64);
	}
	if(tag == (ast_tag.stmt))
	{
		return tag_offset_stmt() + (node->stmt@s64);
	}
	if(tag == (ast_tag.typename))
	{
		return tag_offset_typename() + (node->typename.tag@s64);
	}
	if(tag == (ast_tag.function))
	{
		return tag_offset_function() + (node->function.tag@s64);
	}
	if(tag == (ast_tag.structure))
	{
		return tag_offset_structure() + (node->structure.tag@s64);
	}
	if(tag == (ast_tag.enumeration))
	{
		return tag_offset_enumeration() + (node->enumeration.tag@s64);
	}
	if(tag == (ast_tag.assembly))
	{
		return tag_offset_assembly() + (node->assembly.tag@s64);
	}
	if(tag == (ast_tag.decl))
	{
		return tag_offset_decl() + (node->decl.tag@s64);
	}
	// anything not listed above just uses its ast_tag as its uid
	return node->tag@s64@_;
};

// give me the begin src (DONT PASS ZERO) and a node and i will tell you where the source lives
ast_quote_source ::= func(nodes : ast?, nodes_count : u64, src : u8? mut?, len : u64 mut? -> v0)
{
	first ::= nodes # 0;
	last ::= nodes # (nodes_count - 1);
	if(last->end_cursor < (first->begin_cursor))
	{
		psyc_panic_begin(first->loc);
		putzstr("node span's begin_cursor (");
		putuint(first->begin_cursor);
		putzstr(") > end_cursor(");
		putuint(last->end_cursor);
		putzstr(") - super illegal");
		psyc_diag_end();
		psyc_exit_failure();
	}
	read_ptr ::= deref src;
	deref(src) = (read_ptr # (first->begin_cursor));
	deref(len) = (last->end_cursor - (first->begin_cursor));
};

ast_rotate_left ::= func(node_array : ast mut?, node_array_count : u64 mut?, pivot : u64, shift_amount : u64 -> v0)
{
	// say our nodes are:
	// 0, 1, 2, 3, 4, 5, 6
	// the pivot represents some index in this list
	// we overwrite 'shift_amount' elements # and rightwards of the pivot by the same number of proceeding elements
	// so: ast_rotate_left(nodes, nodes_len, 3, 2) =>
	// 0, 1, 2, 5, 6	
	copydst ::= node_array # pivot;
	copysrc ::= copydst # shift_amount;

	count ::= deref(node_array_count) - (pivot + shift_amount);
	memcopy(copydst, copysrc, __sizeof(ast) * count);
	deref(node_array_count) = (deref(node_array_count) - shift_amount);
	// note we dont zero out any stuff that is beyond the cursor. we could but we shouldnt need to.
};

// assumes node_array capacity >= (node_array_count + shift_amount)
ast_rotate_right ::= func(node_array : ast mut?, node_array_count : u64 mut?, pivot : u64, shift_amount : u64 -> v0)
{
	// say our nodes are:
	// 0, 1, 2, 3, 4
	// the pivot represents some index in this list
	// the shift_amount will shift all elements # and rightwards of the pivot by that amount, adding extra space which will be zero'd out.
	// so: impl_shift_nodes_right(nodes, 3, 2) =>
	// 0, 1, 2, 0, 0, 3, 4	
	copysrc ::= node_array # pivot;
	copydst ::= copysrc # shift_amount;
	// the copy length is not __sizeof(ast) * shift_amount as that wouldnt shift all nodes
	// it will be node_array_count - (shift_amount + pivot)
	memcopy_backwards(copydst, copysrc, __sizeof(ast) * ((1 + deref(node_array_count)) - (pivot + shift_amount)));
	// zero the hole
	memfill(copysrc, 0, __sizeof(ast) * shift_amount);
	deref(node_array_count) = (deref(node_array_count) + shift_amount);
};

foreach_expr ::= func(callback : func(tag : ast_expr_tag -> v0) -> v0)
{
	i : u64 mut;
	for(i = 0, i < __countof(ast_expr_tag), i = i + 1)
	{
		callback((i + 1)@ast_expr_tag);
	}
};

foreach_stmt ::= func(callback : func(tag : ast_stmt_tag -> v0) -> v0)
{
	i : u64 mut;
	for(i = 0, i < __countof(ast_stmt_tag), i = i + 1)
	{
		callback((i + 1)@ast_stmt_tag);
	}
};

isdigit ::= func(ch : u8 -> bool)
{
	return (ch >= '0') && (ch <= '9');
};

interpret_numeric_literal ::= func(src : u8?, lexeme : lex_slice -> ast_literal)
{
	src_begin : u8? mut := src # (lexeme.off);
	begin_cpy ::= src_begin;
	i : u64 mut := 0;

	sign : s64 mut := 1;
	if(deref(src_begin) == '-')
	{
		sign = -1;
		src_begin = src_begin # 1;
		i = i + 1;
	}

	base : s64 mut := 10;
	ch : u8 mut;
	if(deref(src_begin) == '0')
	{
		src_begin = src_begin # 1;
		ch = deref(src_begin);
		if(ch == 'x')
		{
			base = 16;
			src_begin = src_begin # 1;
			i = i + 2;
		}
		if(ch == 'b')
		{
			base = 2;
			src_begin = src_begin # 1;
			i = i + 2;
		}
	}

	ival : s64 mut := 0;
	fval : f64 mut := 0;

	digit_val : s64 mut := 0;
	frac_div : f64 mut := 1.0;

	is_integral : bool mut := true;
	while(i < (lexeme.len))
	{
		ch = deref(begin_cpy # i);
		if(ch == '.')
		{
			is_integral = false;
		}
		if(isdigit(ch))
		{
			digit_val = (ch - '0')@_;
		}
		if(ch >= 'a')
		{
			if(ch <= 'f')
			{
				digit_val = (10 + (ch - 'a'));
			}
		}
		if(ch >= 'A')
		{
			if(ch <= 'F')
			{
				digit_val = (10 + (ch - 'A'));
			}
		}

		if(digit_val >= base)
		{
			putchar(ch);
			putuint(digit_val);
			psyc_panic(__curloc(), "waaah it all went wrong");
		}

		if(is_integral)
		{
			ival = ((ival * base) + digit_val);
		}
		if(!is_integral)
		{
			if(isdigit(ch))
			{
				frac_div = (frac_div / (base@f64));
				fval = (fval + ((digit_val@f64) * frac_div));
			}
		}

		i = i + 1;
	}

	ret : ast_literal mut;
	if(is_integral)
	{
		ival = ival * sign;
		ret.tag = (ast_literal_tag.integer);
		ret.integer = ival;
	}
	if(!is_integral)
	{
		fval = fval + (ival@f64) * (sign@f64);
		ret.tag = (ast_literal_tag.floating_point);
		ret.floating_point = fval;
	}

	return ret;
};
