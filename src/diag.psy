psyc_stage ::= enum
{
	.setup := 0;
	.lex := 1;
	.parse := 2;
};

[[private]]
current_stage : psyc_stage mut;

psyc_stage_times : f32 mut#4;

psyc_colour_red ::= func() -> v0
{
	putchar(27); // esc character
	putchar('[');
	putchar('3');
    putchar('1');
    putchar('m');
};

psyc_colour_yellow ::= func() -> v0
{
	putchar(27); // esc character
	putchar('[');
	putchar('3');
    putchar('3');
    putchar('m');
};

psyc_colour_blue ::= func() -> v0
{
	putchar(27); // esc character
	putchar('[');
	putchar('3');
    putchar('4');
    putchar('m');
};

psyc_colour_reset ::= func() -> v0
{
	putchar(27); // esc character
	putchar('[');
    putchar('0');
    putchar('m');
};

print_srcloc ::= func(l : srcloc) -> v0
{
	if((l.file) != zero)
	{
		puts("at ");
		puts(l.file);
		putchar(' ');
		puts(l.function);
		putchar('(');
		putuint(l.line);
		putchar(':');
		putuint(l.column);
		putchar(')');
	}
};

psyc_exit_failure ::= func() -> v0
{
	__debugbreak();
};

psyc_error ::= func(loc : srcloc, msg : u8&) -> v0
{
	psyc_colour_red();
	puts(__enumname(current_stage));
	puts(" error ");
	print_srcloc(loc);
	puts(": ");
	puts(msg);
	putchar(10);
	psyc_colour_reset();
};

psyc_panic ::= func(my_srcloc : srcloc, msg : u8&) -> v0
{
	psyc_colour_red();
	puts("panic ");
	print_srcloc(my_srcloc);
	putchar(':');
	putchar(' ');
	puts(msg);
	putchar(10);
	psyc_colour_reset();
	psyc_exit_failure();
};

psyc_fatal_error ::= func(loc : srcloc, msg : u8&) -> v0
{
	psyc_error(loc, msg);
	psyc_exit_failure();
};

psyc_warning ::= func(loc : srcloc, msg : u8&) -> v0
{
	psyc_colour_yellow();
	puts("warning: ");
	puts(msg);
	putchar(10);
	psyc_colour_reset();
};

psyc_message ::= func(loc : srcloc, msg : u8&) -> v0
{
	psyc_colour_blue();
	puts("message: ");
	puts(msg);
	putchar(10);
	psyc_colour_reset();
};

if static(__is_windows)
{
	QueryPerformanceCounter ::= func(lpPerformanceCount : s64 mut&) -> s32 := extern;
	QueryPerformanceFrequency ::= func(lpPerformanceCount : s64 mut&) -> s32 := extern;
}

psyc_gettime ::= func() -> s64
{
	if static(__is_windows)
	{
		ret : s64 mut;
		QueryPerformanceCounter(ref ret);
		return ret;
	}
};

psyc_set_stage ::= func(stage : psyc_stage) -> v0
{
	current_stage = stage;
};

psyc_timed ::= macro(stage : psyc_stage) -> v0
{
	psyc_set_stage(stage);
	defer psyc_set_stage(psyc_stage.setup);
	_f : s64 mut;
	QueryPerformanceFrequency(ref _f);
	_t ::= psyc_gettime();
	_s ::= stage@s64;
	defer _diff ::= ((psyc_gettime() - _t) * 1000000) / _f;
	defer _diff_f32 ::= _diff / 1000000.0;
	defer(deref(psyc_stage_times at _s) = (deref(psyc_stage_times at _s) + _diff_f32));
};

psyc_report_a_timing ::= func(stage : psyc_stage) -> v0
{
	puts(__enumname(stage));
	puts(": ");
	putfloat(deref(psyc_stage_times at (stage@s64)));
	putchar('s');
	putchar(10);
};

psyc_report_timings ::= func() -> v0
{
	count ::= __sizeof(psyc_stage_times) / __sizeof(deref(psyc_stage_times at 0));
	i : u32 mut;
	for(i = 0, i < count, i = i + 1)
	{
		psyc_report_a_timing(i@psyc_stage);
	}
};

== build ==
{
	add_source_file("io.psy");
}
